(* Copyright 2016 Yoann Padioleau, see copyright.txt *)
open Common

open Ast_asm
open Ast_asm5
module T = Types
module T5 = Types5

(*****************************************************************************)
(* Helpers *)
(*****************************************************************************)

(* "Names", modifies global, modifies h *)
let process_global (global : Ast_asm.global) h (idfile : int) : unit =
  (match global.priv with
  | Some _ -> global.priv <- Some idfile
  | None -> ()
  );
  (* populate symbol table with SXref if new entity *)
  T5.lookup_global global h |> ignore

(* Visit globals to populate symbol table with wanted symbols.
 * This is more complicated than in 5l because we can not rely
 * on an ANAME or Operand.sym.
 * less: boilerplate which could be auto generated by ocamltarzan in
 *  a visitor_asm5.ml
 *)
let visit_globals f xs =
  let ximm x =
    match x with
    | Address (Global (x, _)) -> f x
    | Address (Param _ | Local _) -> ()
    | String _ -> ()
  in
  let imm_or_ximm x =
    match x with
    | Either.Left _ -> ()
    | Either.Right x -> ximm x
  in
  let mov_operand x =
    match x with
    | Entity (Global (x, _)) -> f x
    | Entity (Param _ | Local _) -> ()
    | Ximm x -> ximm x
    | Imsr _ | Indirect _ -> ()
  in
  let branch_operand x =
    match !x with
    | SymbolJump ent -> f ent
    | IndirectJump _ | Relative _ | LabelUse _ | Absolute _ -> ()
  in
  xs |> List.iter (fun (x, _line) ->
    match x with
    | Pseudo y ->
      (match y with
      | TEXT (ent, _, _) -> f ent
      | GLOBL (ent, _, _) -> f ent
      | DATA (ent, _, _, ix) -> f ent; imm_or_ximm ix
      | WORD (ix) -> imm_or_ximm ix
      )
    | Instr (i, _cond) ->
      (match i with
      | MOVE (_, _, m1, m2) -> mov_operand m1; mov_operand m2
      (* ocaml-light: | B b | BL b | Bxx (_, b) -> branch_operand b *)
      | B b -> branch_operand b
      | BL b -> branch_operand b
      | Bxx (_, b) -> branch_operand b
      | Arith _ | SWAP _ | RET | Cmp _ | SWI _ | RFE | NOP -> () 
      )
    | LabelDef _ -> ()
  )


(*****************************************************************************)
(* Entry point *)
(*****************************************************************************)

(* load() performs a few things:
 * - load objects (of course),
 * - split and concatenate in code vs data all objects,
 * - relocate absolute jumps,
 * - "name" entities by assigning a unique name to every entities
 *   (handling private symbols),
 * - visit all entities (defs and uses) and add them in symbol table
 * alt: take as a parameter (xs : Chan.i list);
 *)
let load (caps : < Cap.open_in; ..>) (xs : Fpath.t list) : T5.code array * T5.data list * Types.symbol_table=

  (* values to return *)
  let code = ref [] in
  let data = ref [] in
  let h = Hashtbl.create 101 in

  let pc : Types.virt_pc ref = ref 0 in
  let idfile = ref 0 in

  xs |> List.iter (fun file ->
    let ipc : Types.virt_pc = !pc in
    incr idfile;
    (* less: assert it is a .5 file *)
    (* TODO: if lib file! *)

    (* object loading is so much easier in ocaml :) *)
    let (prog, _srcfile) = file |> FS.with_open_in caps Object5.load in
    (* less: could check valid AST, range of registers, shift values, etc *)

    (* naming and populating symbol table h *)
    prog |> visit_globals (fun x -> process_global x h !idfile);

    (* split and concatenate in code vs data, relocate branches, 
     * and add definitions in symbol table h.
     *)
    prog |> List.iter (fun (p, line) ->
      match p with
      | Pseudo pseudo ->
          (match pseudo with
          | TEXT (global, attrs, size) ->
              (* less: set curtext for better error managment *)
              let v = T5.lookup_global global h in
              (match v.T.section with
              | T.SXref -> v.T.section <- T.SText !pc;
              | _ -> failwith (spf "redefinition of %s" global.name)
              );
              (* less: adjust autosize? *)
              code |> Stack_.push (T5.TEXT (global, attrs, size), (file, line));
              incr pc;
          | WORD v ->
              code |> Stack_.push (T5.WORD v, (file, line));
              incr pc;
            
          | GLOBL (global, _attrs, size) -> 
              let v = T5.lookup_global global h in
              (match v.T.section with
              | T.SXref -> v.T.section <- T.SData size;
              | _ -> failwith (spf "redefinition of %s" global.name)
              );
          | DATA (global, offset, size, v) -> 
              data |> Stack_.push (T5.DATA (global, offset, size, v))
          )

      | Instr (inst, cond) ->

          let relocate_branch opd =
            match !opd with
            | SymbolJump _ | IndirectJump _ -> ()
            | Relative _ | LabelUse _ ->
                raise (Impossible "Relative or LabelUse resolved by assembler")
            | Absolute i -> opd := Absolute (i + ipc)
          in

          (match inst with
          (* ocaml-light: | B opd | BL opd | Bxx (_, opd) -> relocate_branch opd *)
          | B opd -> relocate_branch opd
          | BL opd -> relocate_branch opd
          | Bxx (_, opd) -> relocate_branch opd
          | _ -> ()
          );
          code |> Stack_.push (T5.I (inst, cond), (file, line));
          incr pc;

      | LabelDef _ -> failwith (spf "label definition in object")
    );
  );
  Array.of_list (List.rev !code), List.rev !data, h
