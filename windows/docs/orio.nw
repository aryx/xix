\documentclass[12pt]{report}

\input{latex/Packages}
\input{latex/Noweb}
\input{latex/Config}
\input{latex/Macros}

%******************************************************************************
% Prelude
%******************************************************************************

%------------------------------------------------------------------------------
%history: 
%------------------------------------------------------------------------------

%thx to LP, I changed for the better a few things:

%thx to this manual, I better understand rio/windowing-system:

%history LP-ization:

\begin{document}
%******************************************************************************
% Title
%******************************************************************************
\title{
{\Huge 
Principia Softwarica: The Plan~9 Windowing System [[rio]]
}\\
OCaml edition\\
{version 0.1}
}

\author{
Yoann Padioleau\\
\texttt{yoann.padioleau@gmail.com}\\
\\
with code from\\
Rob Pike and Yoann Padioleau
}

\maketitle 

%\onecolumn
\hrule
\begin{quote}
Copyright \copyright{} 2025-2026 Yoann Padioleau \\
Permission is granted to copy, distribute and/or modify this document,
except all the source code it contains, under the terms of the GNU Free
Documentation License, Version 1.3.
\end{quote}
\hrule
%\twocolumn

\begingroup
\hypersetup{linkcolor=blue}
% need to s/onecolumn/twocolumn in report.cls :) for \tableofcontents
\tableofcontents
\endgroup


%******************************************************************************
\chapter{Introduction}
%******************************************************************************

#include "Intro.nw"

%\chapter{Overview}
% now in Intro.nw too

\section{[[hellorio.ml]]}

<<constant [[Hellorio._]]>>=
let _ =
  Cap.main (fun caps -> thread_main caps)
@

<<function [[Hellorio.thread_main]]>>=
(* the Keyboard.init() and Mouse.init() below create other threads *)
let thread_main (caps : < Cap.draw; Cap.open_in; Cap.keyboard; Cap.mouse; .. >) =
  let display : Display.t = Draw.init caps "Hello Rio" in
  let view : Display.image = Draw_rio.get_view caps display in

  let kbd : Keyboard.ctl = Keyboard.init caps in
  let mouse : Mouse.ctl = Mouse.init caps in

  let bgcolor : Image.t  = 
    Image.alloc display Rectangle.r_1x1 Channel.rgba32 true Color.magenta
  in
  (* if does not use originwindow, then need to add view.r.min *)
  let mousepos = ref (Point.add view.r.Rectangle.min (Point.p 10 10)) in

  redraw display view !mousepos bgcolor;

  while true do
    <<[[Hellorio.thread_main()]] in event loop>>
    redraw display view !mousepos bgcolor;
  done
@
% keyboard.init will turn rawon on /dev/consctl

<<function [[Hellorio.redraw]]>>=
let redraw (display : Display.t) (view : Display.image) (pos : Point.t)
     (bgcolor : Image.t) : unit =
  Draw.draw view view.r bgcolor None Point.zero;
  (* todo: Text.string *)
  Line.line view pos (Point.add pos (Point.p 100 100))
    Line.EndSquare Line.EndSquare 2 display.black Point.zero;
  Display.flush display
@
%ocaml: less globals, pass down view and display

<<type [[Hellorio.event]]>>=
type event =
  | Mouse of Mouse.state
  | Key of Keyboard.key
  (* less: Resize *)
@

<<[[Hellorio.thread_main()]] in event loop>>=
let ev : event = 
  [
    Keyboard.receive kbd |> (fun ev -> Event.wrap ev (fun x -> Key x));
    Mouse.receive mouse |> (fun ev -> Event.wrap ev (fun x -> Mouse x));
  ] |> Event.select
in
(match ev with
| Mouse m -> 
  mousepos := m.pos
| Key c ->
  if c = 'q'
  then exit 0
  else Logs.app (fun m -> m "%c" c)
(* less: 
 * | Resize -> view := getwindow display
 *)
);
@
% both receive, but can also select on send! so whatever ready to
% receive or send!


\section{Code organization}
\label{tab:code-orga}

\section{Software architecture}

\subsection{Threads relationships}%%Processes,  procs, and threads relationships}
\label{sec:thread-archi}
\label{fig:soft-archi-threads}
\label{fig:hellorio-and-shell}

\subsection{Trace of a new window creation}
\label{sec:trace-new-window}
\label{sec:new-namespace}
\label{sec:new-thread}
\label{sec:new-layer}

\subsection{Trace of a mouse click}
\label{sec:trace-mouse-click}

\subsection{Trace of a key press}
\label{sec:trace-key-press}

\subsection{Trace of a drawing operation}
\label{sec:trace-draw}

\section{Book structure}

%###############################################################################


%******************************************************************************
\chapter{Core Data Structures}
%******************************************************************************
\label{chap:core-ds}

\section{Device handles}

\subsection{Output device: [[display]] and [[view]]}
%ocaml: not globals anymore, passed down functions

\subsection{Input devices: [[mouse]] and [[kbd]]}%%[[mousectl]] and [[keyboardctl]]}
%ocaml: also not globals anymore! threads can be passed arguments!

\section{Desktop}%%, [[desktop]]}
%ocaml: also not a global anymore

\section{Windows}
\label{sec:core-ds-Windows}

\subsection{[[Window.t]]}

<<type [[Window.t]]>>=
(* The window type! *)
type t = {
  (* ---------------------------------------------------------------- *)
  (* ID *)
  (* ---------------------------------------------------------------- *)
  <<[[Window.t]] id fields>>

  (* ---------------------------------------------------------------- *)
  (* Graphics *)
  (* ---------------------------------------------------------------- *)
  <<[[Window.t]] graphics fields>>

  (* ---------------------------------------------------------------- *)
  (* Mouse *)
  (* ---------------------------------------------------------------- *)
  <<[[Window.t]] mouse fields>>

  (* ---------------------------------------------------------------- *)
  (* Keyboard *)
  (* ---------------------------------------------------------------- *)
  <<[[Window.t]] keyboard fields>>

  (* ---------------------------------------------------------------- *)
  (* Commands *)
  (* ---------------------------------------------------------------- *)
  <<[[Window.t]] command fields>>

  (* ---------------------------------------------------------------- *)
  (* Process *)
  (* ---------------------------------------------------------------- *)
  <<[[Window.t]] process fields>>

  (* ---------------------------------------------------------------- *)
  (* Config *)
  (* ---------------------------------------------------------------- *)
  <<[[Window.t]] config fields>>

  (* ---------------------------------------------------------------- *)
  (* Wm *)
  (* ---------------------------------------------------------------- *)
  <<[[Window.t]] wm fields>>

  (* ---------------------------------------------------------------- *)
  (* Textual Window *)
  (* ---------------------------------------------------------------- *)
  <<[[Window.t]] textual window fields>>

  (* ---------------------------------------------------------------- *)
  (* Graphical Window *)
  (* ---------------------------------------------------------------- *)
  <<[[Window.t]] graphical window fields>>

  (* ---------------------------------------------------------------- *)
  (* Concurrency *)
  (* ---------------------------------------------------------------- *)
  (* less: 
   * - a Ref (Mutex.t? atomic anyway in ocaml), ref counting
   *   or simply a counter as there is no race issue for rio-ocaml.
   * - Qlock (Condition.t?), needed for?
   *)

  (* ---------------------------------------------------------------- *)
  (* Misc *)
  (* ---------------------------------------------------------------- *)
  <<[[Window.t]] other fields>>
}
@

<<type [[Window.wid]]>>=
(* window id *)
type wid = int
@


<<[[Window.t]] id fields>>=
(* visible in /mnt/wsys/winid (and used for /mnt/wsys/<id>/devs) *)
id: wid;
@
<<global [[Window.wid_counter]]>>=
let wid_counter = 
  ref 0
@

<<[[Window.t]] id fields>>=
(* writable through /mnt/wsys/label *)
mutable label: string;
@


<<[[Window.t]] id fields>>=
(* public named image, visible in /mnt/wsys/winname; change when resize *)
mutable winname: string;
@
% id field and graphics field actually



<<[[Window.t]] graphics fields>>=
(* This is most of the time a layer, but it can also be a plain Image.t
 * when the window is hidden.
 * less: option? when delete the window structure and thread is still
 * out there because we wait for the process to terminate?
 *)
mutable img: Image.t;
@

<<[[Window.t]] graphics fields>>=
(* todo: for originwindow and really virtual screen? vs img.r? *)
mutable screenr: Rectangle.t;
@



%XXX
<<[[Window.t]] mouse fields>>=
(* Note that we do not queue all mouse states; just the clicks/releases,
 * otherwise the queue would be too big when you move around the mouse.
 * less: max size = ? mutex around? recent queue.mli says not thread-safe 
 *)
mouseclicks_queue: (Mouse.state * mouse_counter) Queue.t;
@

%XXX
<<[[Window.t]] keyboard fields>>=
(* Threads_window.thread --> Thread_fileserver.dispatch(Read).
 * The first channel will be used by thread_fileserver to indicate the
 * number of bytes the process wants to read from its /dev/cons. The second
 * channel will be used to send the bytes to thread_fileserver.
 * Note that we send bytes, even though we read keys.
*)
chan_devcons_read: (int Event.channel * string Event.channel) Event.channel;
@
<<[[Window.t]] keyboard fields>>=
(* Threads_window.thread --> Thread_fileserver.dispatch(Write).
 * Note that we send full runes, not bytes.
 * The channel inside will be used to read from thread_fileserver(Write)
 * the data the process wrote to its /dev/cons.
 *)
chan_devcons_write: (Rune.t list Event.channel) Event.channel;
@

\subsection{[[windows]]}

<<global [[Globals.windows]]>>=
let windows: (Window.wid, Window.t) Hashtbl.t = Hashtbl_.create ()
@

% move later? quite important to understand so better here probably
<<type [[Window.topped_counter]]>>=
type topped_counter = int
@

<<global [[Window.topped_counter]]>>=
let topped_counter =
  ref 0
@

<<[[Window.t]] wm fields>>=
mutable topped: topped_counter;
@

\subsection{[[current]]}
\label{sec:current}

<<global [[Globals.current]]>>=
(* the man page of rio (rio(1)) uses the term 'current' 
 * old: was called 'input' in rio
 *)
let current: Window.t option ref  = ref None
@

<<function [[Globals.win]]>>=
(* a bit like cpu(), up() in the kernel, a convenient global *)
let win () =
  !current
@

\subsection{Graphical windows}

<<[[Window.t]] graphical window fields>>=
mutable mouse_opened: bool;
@

% can also have raw_mode in textual window but the real use is
% for graphical apps
<<[[Window.t]] graphical window fields>>=
mutable raw_mode: bool;
@

<<[[Window.t]] keyboard fields>>=
(* see also Window.terminal below for keys when in non-raw (buffered) mode *)
raw_keys: Keyboard.key Queue.t;
@


<<[[Window.t]] graphical window fields>>=
(* can also be used in textual windows, but more rare *)
mutable consctl_opened: bool;
@

%meta:
% note that rio itself in main call Keyboard.init which turns
% on raw mode and access /dev/consctl, but the real one! (unless
% when rio is run under rio)



\subsection{Textual windows}

<<[[Window.t]] textual window fields>>=
terminal: Terminal.t;
@
%ocaml: new intermediate type instead of putting all fields in Window.t

<<type [[Terminal.t]]>>=
type t = {
  (* the model *)

  <<[[Terminal.t]] text data fields>>
  <<[[Terminal.t]] text cursor fields>>

  (* the view *)

  <<[[Terminal.t]] graphics fields>>

  (* misc *)

  <<[[Terminal.t]] other fields>>
}
@


<<[[Terminal.t]] text data fields>>=
(* growing array (simpler than a gap buffer).
 * alt: a growing string, like in Efuns.
 *)
mutable text: Rune.t array;
(* number of runes used in text (<= Array.length term.text) *)
mutable nrunes: int;

(* less: lines? like in Efuns? with EOF sentinel to simplify code? *)
@

<<[[Terminal.t]] text cursor fields>>=
(* where entered text go (and selection start) (old: q0 in rio) *)
mutable cursor: position;
mutable end_selection: position option;      (* old: q1 in rio) *)
@

<<type [[Terminal.position]]>>=
type position = {
  i: int;
}
@
<<constant [[Terminal.zero]]>>=
let zero = 
  { i = 0 }
@

<<[[Terminal.t]] text cursor fields>>=
(* Division between characters the host has seen and characters not 
 * yet transmitted. The position in the text that separates output from input.
 * old: qh in rio
 *)
mutable output_point: position;
@



<<[[Terminal.t]] graphics fields>>=
img: Image.t;

(* img.r without border and some extra space *)
r: Rectangle.t;
(* right side of r, and no bottom rectangle covering a line of
 * text with font below 
 *)
textr: Rectangle.t;
(* left side of r *)
scrollr: Rectangle.t;
@


%XXX
<<[[Terminal.t]] other fields>>=
(* first character visible in window from 'text' *)
mutable origin_visible: position;
mutable runes_visible: int;
@
<<[[Terminal.t]] other fields>>=
font: Font.t;
@
<<[[Terminal.t]] other fields>>=
(* this will alter which color to use to render the text *)
mutable is_selected: bool;
@
<<[[Terminal.t]] other fields>>=
(* alt: mutable colors: colors; *)
@


\section{Filesystem server}


\subsection{[[Fileserver.t]]}%% and [[filsys]]}
%ocaml: no filsys global

<<type [[Fileserver.t]]>>=
type t = {
  (* the pipe *)

  (* clients_fd will be shared by all the winshell processes *)
  clients_fd: Unix.file_descr;
  server_fd: Unix.file_descr;

  (* for security *)
  user: string;

  (* the files managed by the server currently-in-use by the client *)
  fids: (File.fid, File.t) Hashtbl.t;

  <<[[Fileserver.t]] other fields>>
}
@


\subsection{File state and file ids}

<<type [[File.fid]]>>=
(* This is maintained by the "client" (the kernel on behalf of a winshell) *)
type fid = Protocol_9P.fid
@

<<type [[File.t]]>>=
(* fid server-side state (a file) *)
type t = {
  (* The fid is maintained by the "client" (the kernel on behalf of winshell).
   * It is the key used to access information about a file used by
   * the client (it's redundant in File.t because it is also the key in
   * Fileserver.t.fids)
   *)
  fid: fid;

  (* the state *)
  mutable opened: Plan9.open_flags option;

  <<[[File.t]] other fields>>
  (* less: nrpart for runes *)
}
@

<<[[File.t]] other fields>>=
(* for stat (mutable for the same reason than qid) *)
mutable entry: dir_entry_short;
@

<<type [[File.dir_entry_short]]>>=
(* simpler than Plan9.dir_entry *)
type dir_entry_short = { 
  name: string;
  code: filecode;

  type_: Plan9.qid_type;
  (* just for the user; group and other are 'noperm' *)
  perm: Plan9.perm_property;
}
@

<<type [[File.filecode]]>>=
(* This is returned by the server (rio) to identify a file of the server *)
type filecode = 
  | Dir of dir
  | File of devid
@
<<type [[File.dir]]>>=
  and dir = 
    (* '/' 
     * old: was called Qdir in rio *)
    | Root
@

<<constant [[File.root_entry]]>>=
let root_entry = 
  { name = "."; code = Dir Root; type_ = N.QTDir; perm =  N.rx }
@



<<[[File.t]] other fields>>=
(* less: we could also use a wid *)
win: Window.t;
@
% get back the window of the process doing the request! very important!
% how do we get the window? during Attach and in Plan9.mount call
% we pass the window id to mount, which is then seen in Attach for
% the rootfid



%%\subsection{Workers and jobs: [[Xfid]]}
%%\subsection{9P callbacks: [[fcall]]}

\section{Devices}
%ocaml: new section, new type

<<type [[File.devid]]>>=
  and devid = 
    | WinName
    | Mouse
    (* todo: Cursor ... *)
    | Cons
    | ConsCtl

    | WinId
    | Text
@

<<constant [[Threads_fileserver.all_devices]]>>=
let all_devices : (File.devid * Device.t) list = [
  File.WinName , Virtual_draw.dev_winname;
  File.Mouse   , Virtual_mouse.dev_mouse;
  File.Cons    , Virtual_cons.dev_cons;
  File.ConsCtl , Virtual_cons.dev_consctl;
  
  File.WinId   , Dev_wm.dev_winid;
  File.Text    , Dev_textual_window.dev_text;
]
@
<<constant [[Threads_fileserver.toplevel_entries]]>>=
let toplevel_entries : File.dir_entry_short list =
  all_devices |> List.map (fun (devid, dev) ->
    File.{
      name = dev.D.name;
      code = File.File devid;
      type_ = Plan9.QTFile;
      perm = dev.D.perm;
    }
  )
@
% used for Walk


<<function [[Threads_fileserver.device_of_devid]]>>=
let device_of_devid (devid : File.devid) : Device.t =
  try 
    List.assoc devid all_devices
  with Not_found ->
    raise (Impossible (spf "all_devices is not correctly set; missing code %d"
             (File.int_of_filecode (File.File devid))))
@


<<type [[Device.t]]>>=
type t = {
  (* ex: "winname", "cons" *)
  name: string;
  perm: Plan9.perm_property;

  (* called when a process is opening/closing the device *)
  open_: Window.t -> unit;
  close: Window.t -> unit;
  
  (* called when a process is reading/writing on the device.
   *
   * we need to thread 'read' (and 'write') because this operation may spend
   * some time waiting while receiving and sending information on channels.
   * 
   * Note that 'read' takes an offset (and count) because each device 
   * can honor or not the offset requirements. For instance,
   * /dev/winname does honor offset but /dev/mouse does not.
   *)
  read_threaded: int64 -> int -> Window.t -> string (* bytes *);
  write_threaded: int64 -> string (* bytes *) -> Window.t -> unit;
}
@
% methods

<<constant [[Device.default]]>>=
let default = {
  name = "<default>";
  perm = Plan9.rw;
  open_ = (fun _ -> ());
  close = (fun _ -> ());
  read_threaded = (fun _ _ _ -> "");
  write_threaded = (fun _ _ _ -> ());
}
@


%******************************************************************************
\chapter{[[main()]]}
%******************************************************************************
\label{chap:main}

<<toplevel [[Main._1]]>>=
let _ = 
  Cap.main (fun (caps : Cap.all_caps) ->
     Exit.exit caps (Exit.catch (fun () -> CLI.main caps (CapSys.argv caps)))
  )
@


<<type [[CLI.caps]]>>=
(* Need:
 * - draw/mouse/keyboard because rio multiplexes access to those devices
 * - fork/exec/chdir when creating new windows which trigger new rc
 *   processes run possibly from different directories.
 * - open_in: for /dev/winname access
 * - mount/bind: for the window to mount the rio fileserver to /mnt/wsys
 *   and then bind it to /dev for virtual /dev/{cons,mouse,...}
 *)
type caps = < 
    Cap.draw; Cap.mouse; Cap.keyboard;
    Cap.fork; Cap.exec; Cap.chdir;
    Cap.open_in;
    Cap.mount; Cap.bind
  >
@

<<signature [[CLI.main]]>>=
(* entry point (can also raise Exit.ExitCode) *)
val main: <caps; Cap.stdout; Cap.stderr; ..> ->
  string array -> Exit.t
@

<<constant [[CLI.usage]]>>=
let usage = 
  "usage: rio [options]"
@


<<function [[CLI.main]]>>=
let main (caps : < caps; Cap.stdout; Cap.stderr; ..>) (argv : string array) :
    Exit.t =
  <<[[CLI.main()]] locals>>
  let options = [
    <<[[CLI.main()]] [[options]] elements>>
  ] |> Arg.align
  in
  (* This may raise ExitCode *)
  Arg_.parse_argv caps argv options (fun _f -> Arg.usage options usage) usage;
  <<[[CLI.main()]] logging setup>>
  try 
    (* the main call *)
    thread_main caps
  with exn ->
      <<[[CLI.main()]] handle [[exn]]>>
@


<<signature [[CLI.thread_main]]>>=
(* main thread which will itself create mouse/keyboard/fs/... threads *)
val thread_main: < caps; ..> -> Exit.t
@

<<function [[CLI.thread_main]]>>=
let thread_main (caps: < caps; .. >) : Exit.t =

  (* Rio, a graphical application *)
  <<[[CLI.thread_main()]] graphics initializations>>

  (* Rio, a filesystem server *)
  let fs = Fileserver.init () in

  (* Rio, a concurrent application *)
  let (exit_chan: Exit.t Event.channel) = Event.new_channel () in
  <<[[CLI.thread_main()]] threads creation>>

  (* Wait *)
  let exit_code = Event.receive exit_chan |> Event.sync in
  (* todo: kill all procs? all the winshell processes? *)
  (* todo: kill all threads? done when do exit no? *)
  exit_code
@

\section{Graphics initialization}
% rio a graphical app!

% similar start to hellorio.ml

<<[[CLI.thread_main()]] graphics initializations>>=
let display : Display.t = Draw.init caps "orio" in
(* alt: simpler (but does not allow rio under rio in): 
 * let view = display.image in
 * less: let viewr save?
 *)
let view : Display.image = Draw_rio.get_view caps display in
let font : Font.t = Font_default.load_default_font display in

<<[[CLI.thread_main()]] graphics initializations, debug>>

let background = Image.alloc_color display (Color.mk2 0x77 0x77 0x77) in
Globals.red   := Image.alloc_color display (Color.mk2 0xDD 0x00 0x00);
Globals.title_color       := Image.alloc_color display Color.greygreen;
Globals.title_color_light := Image.alloc_color display Color.palegreygreen;

let desktop : Baselayer.t = Baselayer.alloc view background in

Draw.draw_color view view.r background;
  (* to test: alternative to -test that leverages work done above
  Test.test_display_default_font display view;
  Test.test_display_text display view font;
  *)

Display.flush display;
@
% desktop! baselayer on top of which can put layers
% useful for?

<<constant [[Globals.red]]>>=
let red               = ref Display.fake_image
@
<<constant [[Globals.title_color]]>>=
let title_color       = ref Display.fake_image
@
<<constant [[Globals.title_color_light]]>>=
let title_color_light = ref Display.fake_image
@

%%\section{Mouse initialization}
%%\section{Keyboard initialization}
%%\section{Channels creation}

\section{Threads creation}
% rio a concurrent app!
% those keyboard/mouse threads will read raw keys and mouse events
% from the device, from the kernel! (not virtual here, unless rio under rio)
% Keyboard.init will open /dev/consctl and turn raw on

% again similar to hellorio.ml
<<[[CLI.thread_main()]] threads creation>>=
let mouse : Mouse.ctl = Mouse.init caps in
let kbd : Keyboard.ctl = Keyboard.init caps in

let _kbd_thread   = 
  Thread.create Thread_keyboard.thread kbd in

let _mouse_thread = 
  Thread.create (Thread_mouse.thread caps)
     (exit_chan, 
      mouse, (display, desktop, view, font),
      fs) in

let _fileserver_master_thread =
  Thread.create Threads_fileserver.thread fs in
@

\section{Filesystem server initialization}

%\subsection{[[Fileserver.init()]]}%%[[filsysinit()]]}

<<signature [[Fileserver.init]]>>=
(* internally creates a pipe between clients_fd/server_fd above *)
val init: unit -> t
@

<<function [[Fileserver.init]]>>=
let init () =
  let (fd1, fd2) = Unix2.pipe () in
  (* the default threadUnix implementation just set non_block for fd2
   * (the 'in_fd'), but in plan9 pipes are bidirectional so we need 
   * to set non_block for fd1 too.
   *)
  Unix.set_nonblock fd1;
  (* todo? record fd2 as close_on exec? 
   * Unix.set_close_on_exec? but when it's useful really? just cleaner/safer?
   *)
 
  { clients_fd = fd1;
    server_fd = fd2;

    (* todo: let user = Common.cat "/dev/user" |> String.concat "" in *)
    user = "pad";
    message_size = 8192 + Protocol_9P.io_header_size;

    fids = Hashtbl_.create ();
  }
@

%%\subsection{Worker allocator: [[xfidinit()]]}

%******************************************************************************
\chapter{Threads} %% Procs and Threads
%******************************************************************************
\label{chap:processes-threads}
%ocaml: no Procs and Threads, just threads as Thread do not block
% on unix calls (thx to ThreadUnix) so no need intermediate proc
% to do the read and thread for the channel.

\section{Keyboard thread}

<<signature [[Thread_keyboard.thread]]>>=
(* Reads from the keyboard and sends the key to the "current" window *)
val thread: Keyboard.ctl -> unit
@

<<function [[Thread_keyboard.thread]]>>=
let thread (kbd : Keyboard.ctl) : unit =
  (* less: threadsetname *)

  while true do
    let key = Keyboard.receive kbd |> Event.sync in
    (* less: 
     *  - do that in other thread? so can start reading more keys? 
     *  - have sendp?
     *  - receive array of keys? nbrecv?
     *  - use double array of keys so can send and then receive without
     *    losing anything?
    *)
    Globals.win () |> Option.iter (fun (win : Window.t) ->
      Event.send win.chan_keyboard key |> Event.sync
    )
  done
@


<<[[Window.t]] keyboard fields>>=
(* Threads_window.thread <-- Thread_keyboard.thread (<-- keyboard.thread) *)
(* todo: need list of keys? [20]?not reactif enough if buffer one key only? *)
chan_keyboard: Keyboard.key Event.channel;
@

% will see later then things consuming what is produced here

\section{Mouse thread}

<<signature [[Thread_mouse.thread]]>>=
(* Reads from the mouse and sends the mouse state to the "current" window
 * if the cursor is in the windows rect. Otherwise might call the 
 * window manager to change the current window or also display a menu depending
 * on the mouse state. With this menu one can select "New" which will create
 * a new window and process (hence the need for the many Cap.xxx below).
 *)
val thread: 
  < Cap.fork; Cap.exec; Cap.chdir; Cap.mount; Cap.bind; .. > ->
  Exit.t Event.channel *
  Mouse.ctl *
  (Display.t * Baselayer.t * Image.t * Font.t) *
  Fileserver.t -> 
  unit
@

<<type [[Thread_mouse.event]]>>=
type event =
  | Mouse of Mouse.state
@

<<function [[Thread_mouse.thread]]>>=
let thread (caps : < Cap.fork; .. >) 
           (exitchan, mouse, (display, desktop, view, font), fs) =
  (* less: threadsetname *)

  while true do
    let ev = 
      [
        Mouse.receive mouse |> (fun ev -> Event.wrap ev (fun x -> Mouse x));
      ] |> Event.select
    in
    (match ev with
    | Mouse m ->
      (* less: wkeyboard and button 6 *)
      (* less: loop again *)
      (* less: wkeyboard and ptinrect *)
      (* todo? race on Globals.win? can change between? need store in local?*)

      <<[[Thread_mouse.thread()]] let [[sending_to_win]]>>
      (match sending_to_win with
      | true ->
        <<[[Thread_mouse.thread()]] when [[sending_to_win]]>>
      | false ->
        <<[[Thread_mouse.thread()]] when not [[sending_to_win]]>>
    )
  )
  done
@

\subsection{Application mouse events}

<<[[Thread_mouse.thread()]] let [[sending_to_win]]>>=
let sending_to_win =
  match Globals.win () with
  | Some (w : Window.t) ->
    (* less: logical coordinates with winput.img.r and winput.screenr *)
    let xy = m.pos in
    (* less: goto scrolling if scroll buttons *)
    let inside = Window.pt_inside_border xy w in
    (* todo: set scrolling *)
    (* todo: set moving *)
    (* less: || scrolling *)
    inside && (w.mouse_opened || m.buttons.left)
  | None -> false
in
@

<<[[Thread_mouse.thread()]] when [[sending_to_win]]>>=
(* could assert that Globals.win() <> None *)
Globals.win () |> Option.iter (fun (w : Window.t) ->
  (if not (Mouse.has_click m)
  then Wm.corner_cursor_or_window_cursor w m.pos mouse
  (* todo: why if click then not corner cursor? *)
  else Wm.window_cursor w m.pos  mouse
  );

  (* less: send logical coordinates *)
  Event.send w.chan_mouse m |> Event.sync
)
@

<<[[Window.t]] mouse fields>>=
(* Threads_window.thread <-- Thread_mouse.thread (<-- Mouse.thread) *)
chan_mouse: Mouse.state Event.channel;
@
% again will see later threads that consume what is produced here

<<signature [[Wm.corner_cursor_or_window_cursor]]>>=
val corner_cursor_or_window_cursor: 
  Window.t -> Point.t -> Mouse.ctl -> unit
@

<<signature [[Wm.window_cursor]]>>=
val window_cursor: 
  Window.t -> Point.t -> Mouse.ctl -> unit
@


\subsection{Windowing system mouse events}

<<[[Thread_mouse.thread()]] when not [[sending_to_win]]>>=
let wopt = Globals.window_at_point m.pos in
(match wopt with
| Some w -> Wm.corner_cursor_or_window_cursor w m.pos mouse
| None -> Mouse.reset_cursor mouse
);
(* todo: if moving and buttons *)
(* todo: set corner cursor again part2 *)

if Mouse.has_click m
then 
  let under_mouse =
    <<[[Thread_mouse.thread()]] compute [[under_mouse]]>>
  in
  (match under_mouse with
  <<[[Thread_mouse.thread()]] match [[under_mouse]] cases>>
  | _ -> raise (Impossible "Mouse.has_click so one field is true")
  );
(* todo: reset moving *)
(* less: drain *)
@

<<function [[Globals.window_at_point]]>>=
(* old: was called wpointto in rio *)
let window_at_point pt =
  let res = ref None in
  windows |> Hashtbl.iter (fun _k w ->
    if Rectangle.pt_in_rect pt w.W.screenr && not w.W.deleted
    then
      match !res with
      | None -> res := Some w
      | Some w2 when w.W.topped > w2.W.topped -> res := Some w
      | _ -> ()
  );
  !res
@
% can have multiple windows under the mouse, but we look for the one
% at the top!

<<type [[Thread_mouse.under_mouse]]>>=
type under_mouse =
  | Nothing
  | CurrentWin of Window.t
  | OtherWin   of Window.t
@

<<[[Thread_mouse.thread()]] compute [[under_mouse]]>>=
match wopt, Globals.win () with
| None, _ -> Nothing
(* less: look if w2.topped > 0? seems useless *)
| Some w1, Some w2 when w1 == w2 -> CurrentWin w1
| Some w, _ -> OtherWin w
@


%XXX delete
<<[[Thread_mouse.thread()]] match [[under_mouse]] cases>>=
(* TODO: remove; just because hard to right click on QEMU and laptop*)
| Nothing when m.buttons.left ->
  wm_menu caps m.pos Mouse.Left exitchan 
    mouse (display, desktop, view, font) fs
@

<<[[Thread_mouse.thread()]] match [[under_mouse]] cases>>=
| ((*Nothing |*) CurrentWin _) when m.buttons.left ->
  ()
@

%XXX
<<[[Thread_mouse.thread()]] match [[under_mouse]] cases>>=
| Nothing when m.buttons.middle ->
   middle_click_system m mouse
@
<<[[Thread_mouse.thread()]] match [[under_mouse]] cases>>=
| CurrentWin (w : Window.t) when m.buttons.middle ->
  if not w.mouse_opened
  then middle_click_system m mouse
@
%XXX
<<function [[Thread_mouse.middle_click_system]]>>=
let middle_click_system _m _mouse =
  Logs.err (fun m -> m "Todo: middle click")
@


\section{Window threads}

<<signature [[Threads_window.thread]]>>=
(* Thread listening to keyboard/mouse/wm events for a particular window
 * and dispatching to the right channel.
 *)
val thread: Window.t -> unit
@

<<type [[Threads_window.event]]>>=
type event = 
  (* reading from keyboard thread *)
  | Key   of Keyboard.key
  (* reading from mouse thread *)
  | Mouse of Mouse.state
  (* reading from many places *)
  | Cmd   of Window.cmd

  <<[[Threads_window.event]] other cases>>
@
% those are all 'reading', but we will see later can also have 'sending'

<<function [[Threads_window.thread]]>>=
let thread (w : Window.t) =
  (* less: threadsetname *)
  <<[[Threads_window.thread()]] channels creation>>

  while true do
    let ev = (
    (* receive *)
    [ 
      Event.receive w.chan_keyboard |> wrap (fun x -> Key x);
      Event.receive w.chan_mouse    |> wrap (fun x -> Mouse x);
      Event.receive w.chan_cmd      |> wrap (fun x -> Cmd x);
    ] @
      <<[[Threads_window.thread()]] other [[select]] elements>>
      []
    ) |> Event.select
    in
    (match ev with
    | Key key -> 
      key_in w key
    | Mouse m -> 
      mouse_in w m
    | Cmd cmd -> 
      (* todo: if return Exited then threadsexit and free channels.
       * When answer Exited? when Cmd is Exited?
       *)
      cmd_in w cmd
    <<[[Threads_window.thread()]] match [[ev]] other cases>>
    );
    if not w.deleted
    then Image.flush w.img;
  done
@

% again, those are all Event.receive but in select can also have
% Event.send

<<function [[Threads_window.wrap]]>>=
let wrap f = 
  fun ev -> Event.wrap ev f
@

<<[[Window.t]] command fields>>=
(* Threads_window.thread <-- Thread_mouse.thread? | ?? *)
(* less: also list of cmds? [20]? *)
chan_cmd: cmd Event.channel;
@

<<type [[Window.cmd]]>>=
type cmd =
  <<[[Window.cmd]] cases>>
@

<<[[Window.cmd]] cases>>=
| Delete
@
% more in Section X to actually produce and handle Delete

<<[[Window.t]] other fields>>=
(* todo: why need this? race between delete window and other operation? *)
mutable deleted: bool;
@



%XXX
<<[[Window.cmd]] cases>>=
(*
  | Move of Image.t * Rectangle.t
  | Refresh
  | Wakeup
  (* less: RawOff | RawOn? HoldOn | HoldOff *)
*)
@




%XXX
<<[[Threads_window.thread()]] channels creation>>=
let chan_devcons_read_count = Event.new_channel () in
let chan_devcons_read_bytes = Event.new_channel () in
let chan_devcons_write_runes = Event.new_channel () in
@
%XXX
<<[[Threads_window.event]] other cases>>=
(* producing for thread_fileserver(Read Qcons) *)
| SentChannelsForConsRead
@
<<[[Threads_window.event]] other cases>>=
(* producing and then consuming for thread_fileserver(Write Qcons) *)
| SentChannelForConsWrite
@

%XXX
<<[[Threads_window.thread()]] other [[select]] elements>>=
(* less: npart *)
(if (w.raw_mode && Queue.length w.raw_keys > 0) ||
    (not w.raw_mode && Terminal.newline_after_output_point w.terminal)
 then [Event.send w.chan_devcons_read 
          (chan_devcons_read_count, chan_devcons_read_bytes)
        |> wrap (fun () -> SentChannelsForConsRead)]
 else []
) @
@
<<[[Threads_window.thread()]] other [[select]] elements>>=
(* less: auto_scroll, mouseopen?? 
 * todo: qh vs org and nchars *)
(if true
 then [Event.send w.chan_devcons_write chan_devcons_write_runes
      |> wrap (fun () -> SentChannelForConsWrite);]
 else []
) @
@
%XXX
<<[[Threads_window.thread()]] match [[ev]] other cases>>=
| SentChannelsForConsRead -> 
  bytes_out w (chan_devcons_read_count, chan_devcons_read_bytes)
@

%XXX
<<[[Threads_window.thread()]] match [[ev]] other cases>>=
| SentChannelForConsWrite ->
  runes_in w chan_devcons_write_runes
@
<<function [[Threads_window.runes_in]]>>=
(* output from application *)
let runes_in (w: Window.t) (chan : Rune.t list Event.channel) =
  let runes = Event.receive chan |> Event.sync in
  Terminal.runes_in w.terminal runes
@
<<signature [[Terminal.runes_in]]>>=
val runes_in: t -> Rune.t list -> unit
@

\subsection{Keyboard events listening}

<<function [[Threads_window.key_in]]>>=
(* input from user *)
let key_in (w : Window.t) (key : Keyboard.key) =
  (* less: if key = 0? when can happen? EOF? Ctrl-D? *)
  if not w.deleted then begin
    match w.raw_mode, w.mouse_opened with
    <<[[Threads_window.key_in()]] match [[raw_mode]] and [[mouse_opened]] cases>>
  end
@

<<[[Threads_window.key_in()]] match [[raw_mode]] and [[mouse_opened]] cases>>=
(* todo: if holding *)
| false, false -> 
  (* less: snarf *)
  Terminal.key_in w.terminal key
@
<<signature [[Terminal.key_in]]>>=
val key_in: t -> Keyboard.key -> unit
@
% will see later


<<[[Threads_window.key_in()]] match [[raw_mode]] and [[mouse_opened]] cases>>=
| true, true (* less: || q0 == nr *) ->
  Queue.add key w.raw_keys 
@
% raw_keys!


%XXX
<<[[Threads_window.key_in()]] match [[raw_mode]] and [[mouse_opened]] cases>>=
(* less: in theory we should allow also special navigation keys here *)
| true, false  ->
  failwith "key_in: TODO: raw mode in textual window"
@
<<[[Threads_window.key_in()]] match [[raw_mode]] and [[mouse_opened]] cases>>=
| false, true  ->
  failwith "key_in: TODO: buffered mode in graphical window"
@

\subsection{Mouse events listening}

<<function [[Threads_window.mouse_in]]>>=
let mouse_in (w : Window.t) (m : Mouse.state) =
  w.last_mouse <- m;
  match w.mouse_opened with
  | true -> 
    w.mouse_counter <- w.mouse_counter + 1;
    (* less: limit queue length? *)
    if m.buttons <> w.last_buttons 
    then begin 
      Queue.add (m, w.mouse_counter) w.mouseclicks_queue;
      w.last_buttons <- m.buttons
    end;

  | false -> failwith "mouse_in: mouse not opened todo"
@

<<[[Window.t]] mouse fields>>=
(* we do not queue all mouse states (we queue just the clicks/releases);
 * for the rest (moving the mouse) we just keep the last state.
 *)
mutable last_mouse: Mouse.state;

@
<<[[Window.t]] mouse fields>>=
(* ?? how differ from last_mouse.buttons? *)
mutable last_buttons: Mouse.buttons;
@

<<[[Window.t]] mouse fields>>=
(* less: could have simpler mouse_new_event: bool? *)
mutable mouse_counter: mouse_counter;
@
% useful for?

%XXX
<<[[Window.t]] mouse fields>>=
mutable last_count_sent: mouse_counter;
@


\subsection{Control events listening}

<<function [[Threads_window.cmd_in]]>>=
let cmd_in (w : Window.t) (cmd : cmd) =
  match cmd with
  <<[[Threads_window.cmd_in()]] match [[cmd]] cases>>
@


\section{Filesystem server thread} %% proc}

%%\subsection{[[filsysproc()]]}

<<signature [[Threads_fileserver.thread]]>>=
(* 
  will serve all the virtual devices for new windows/processes created by rio.
 *)
val thread: Fileserver.t -> unit
@

<<function [[Threads_fileserver.thread]]>>=
(* the master *)
let thread (fs : Fileserver.t) =
  (* less: threadsetname *)
  
  while true do
    let req : P9.message = P9.read_9P_msg fs.server_fd in

    <<[[Threads_fileserver.thread]] in loop, debug>>

    (match req.typ with
    | P9.T x -> dispatch fs req x
    | P9.R _x ->
      (* less: Ebadfcall *)
      raise (Impossible (spf "got a response request: %s" (P9.str_of_msg req)))
    );
    (* for Version first-message check *)
    first_message := false
  done
@

<<constant [[Threads_fileserver.first_message]]>>=
(* for Version *)
let first_message = ref true
@

<<function [[Threads_fileserver.dispatch]]>>=
let dispatch (fs : Fileserver.t) (req : P9.message) (request_typ : P9.Request.t) =
  match request_typ with
  <<[[Threads_fileserver.dispatch()]] match [[request_typ]] cases>>
@


\subsection{[[Version]] request}%%[[filsysversion()]]}

<<[[Threads_fileserver.dispatch()]] match [[request_typ]] cases>>=
(* Version *)
| T.Version (msize, str) -> 
  (match () with
  | _ when not !first_message ->
    error fs req "version: request not first message"
  | _ when msize < 256 ->
    error fs req "version: message size too small";
  | _ when str <> "9P2000" ->
    error fs req "version: unrecognized 9P version";
  | _ ->
    fs.message_size <- msize;
    answer fs {req with P9.typ = P9.R (R.Version (msize, str)) }
  )
@
% see error in appendix

<<[[Fileserver.t]] other fields>>=
(* refined after Tversion first message *)
mutable message_size: int;
@

<<function [[Threads_fileserver.answer]]>>=
let answer (fs : Fileserver.t) (res : P9.message) =
  <<[[Threads_fileserver.answer()]] debug>>
  P9.write_9P_msg res fs.server_fd
@

%%\section{[[Xfid]] allocator thread}
%%\section{[[Xfid]] threads}

%******************************************************************************
\chapter{Cursors}
%******************************************************************************
\label{chap:cursor}

\section{Cursor graphics}

\subsection{Classic cursors}

<<signature [[Cursors.crosscursor]]>>=
val crosscursor : Cursor.t
@
<<signature [[Cursors.boxcursor]]>>=
val boxcursor   : Cursor.t
@
<<signature [[Cursors.sightcursor]]>>=
val sightcursor : Cursor.t
@

<<constant [[Cursors.crosscursor]]>>=
(* when create a new window (sweep()) *)
let crosscursor = {
  offset = { x = -7; y = -7; };
  clr = Cursor.ints_to_bytes
    [| 0x03; 0xC0; 0x03; 0xC0; 0x03; 0xC0; 0x03; 0xC0;
       0x03; 0xC0; 0x03; 0xC0; 0xFF; 0xFF; 0xFF; 0xFF;
       0xFF; 0xFF; 0xFF; 0xFF; 0x03; 0xC0; 0x03; 0xC0;
       0x03; 0xC0; 0x03; 0xC0; 0x03; 0xC0; 0x03; 0xC0; 
    |];
  set = Cursor.ints_to_bytes
    [| 0x00; 0x00; 0x01; 0x80; 0x01; 0x80; 0x01; 0x80;
       0x01; 0x80; 0x01; 0x80; 0x01; 0x80; 0x7F; 0xFE;
       0x7F; 0xFE; 0x01; 0x80; 0x01; 0x80; 0x01; 0x80;
       0x01; 0x80; 0x01; 0x80; 0x01; 0x80; 0x00; 0x00;
    |];
}
@

<<constant [[Cursors.boxcursor]]>>=
(* when move a window (drag()) *)
let boxcursor = {
  offset =  {x = -7; y = -7; };
  clr = Cursor.ints_to_bytes
    [| 0xFF; 0xFF; 0xFF; 0xFF; 0xFF; 0xFF; 0xFF; 0xFF;
       0xFF; 0xFF; 0xF8; 0x1F; 0xF8; 0x1F; 0xF8; 0x1F;
       0xF8; 0x1F; 0xF8; 0x1F; 0xF8; 0x1F; 0xFF; 0xFF;
       0xFF; 0xFF; 0xFF; 0xFF; 0xFF; 0xFF; 0xFF; 0xFF; 
    |];
  set = Cursor.ints_to_bytes
    [| 0x00; 0x00; 0x7F; 0xFE; 0x7F; 0xFE; 0x7F; 0xFE;
       0x70; 0x0E; 0x70; 0x0E; 0x70; 0x0E; 0x70; 0x0E;
       0x70; 0x0E; 0x70; 0x0E; 0x70; 0x0E; 0x70; 0x0E;
       0x7F; 0xFE; 0x7F; 0xFE; 0x7F; 0xFE; 0x00; 0x00; 
    |];
}
@

<<constant [[Cursors.sightcursor]]>>=
(* when select a window (point_to()) *)
let sightcursor = {
  offset =  {x = -7; y = -7; };
  clr = Cursor.ints_to_bytes
    [| 0x1F; 0xF8; 0x3F; 0xFC; 0x7F; 0xFE; 0xFB; 0xDF;
       0xF3; 0xCF; 0xE3; 0xC7; 0xFF; 0xFF; 0xFF; 0xFF;
       0xFF; 0xFF; 0xFF; 0xFF; 0xE3; 0xC7; 0xF3; 0xCF;
       0x7B; 0xDF; 0x7F; 0xFE; 0x3F; 0xFC; 0x1F; 0xF8; 
    |];
  set = Cursor.ints_to_bytes
    [| 0x00; 0x00; 0x0F; 0xF0; 0x31; 0x8C; 0x21; 0x84;
       0x41; 0x82; 0x41; 0x82; 0x41; 0x82; 0x7F; 0xFE;
       0x7F; 0xFE; 0x41; 0x82; 0x41; 0x82; 0x41; 0x82;
       0x21; 0x84; 0x31; 0x8C; 0x0F; 0xF0; 0x00; 0x00; 
    |];
}
@


<<constant [[Cursors._whitearrow]]>>=
(* for holding mode *)
let _whitearrow = {
  offset =  {x = 0; y = 0; };
  clr = Cursor.ints_to_bytes
    [| 0xFF; 0xFF; 0xFF; 0xFF; 0xFF; 0xFE; 0xFF; 0xFC; 
       0xFF; 0xF0; 0xFF; 0xF0; 0xFF; 0xF8; 0xFF; 0xFC; 
       0xFF; 0xFE; 0xFF; 0xFF; 0xFF; 0xFE; 0xFF; 0xFC; 
       0xF3; 0xF8; 0xF1; 0xF0; 0xE0; 0xE0; 0xC0; 0x40; 
    |];
  set = Cursor.ints_to_bytes
    [| 0xFF; 0xFF; 0xFF; 0xFF; 0xC0; 0x06; 0xC0; 0x1C; 
       0xC0; 0x30; 0xC0; 0x30; 0xC0; 0x38; 0xC0; 0x1C; 
       0xC0; 0x0E; 0xC0; 0x07; 0xCE; 0x0E; 0xDF; 0x1C; 
       0xD3; 0xB8; 0xF1; 0xF0; 0xE0; 0xE0; 0xC0; 0x40; 
    |];
}
@

<<constant [[Cursors._query]]>>=
(* ?? *)
let _query = {
  offset =  {x = -7;y = -7; };
  clr = Cursor.ints_to_bytes
    [| 0x0f; 0xf0; 0x1f; 0xf8; 0x3f; 0xfc; 0x7f; 0xfe; 
       0x7c; 0x7e; 0x78; 0x7e; 0x00; 0xfc; 0x01; 0xf8; 
       0x03; 0xf0; 0x07; 0xe0; 0x07; 0xc0; 0x07; 0xc0; 
       0x07; 0xc0; 0x07; 0xc0; 0x07; 0xc0; 0x07; 0xc0; 
    |];
  set = Cursor.ints_to_bytes
    [| 0x00; 0x00; 0x0f; 0xf0; 0x1f; 0xf8; 0x3c; 0x3c; 
       0x38; 0x1c; 0x00; 0x3c; 0x00; 0x78; 0x00; 0xf0; 
       0x01; 0xe0; 0x03; 0xc0; 0x03; 0x80; 0x03; 0x80; 
       0x00; 0x00; 0x03; 0x80; 0x03; 0x80; 0x00; 0x00; 
    |];
}
@

\subsection{Border and corner cursors}

<<type [[Cursors.corner]]>>=
type corner = 
  | TopLeft    | Top    | TopRight
  | Left                | Right
  | BottomLeft | Bottom | BottomRight
@

<<constant [[Cursors.tl]]>>=
let tl = {
  offset =  {x = -4; y = -4; };
  clr = Cursor.ints_to_bytes
    [| 0xfe; 0x00; 0x82; 0x00; 0x8c; 0x00; 0x87; 0xff; 
       0xa0; 0x01; 0xb0; 0x01; 0xd0; 0x01; 0x11; 0xff; 
       0x11; 0x00; 0x11; 0x00; 0x11; 0x00; 0x11; 0x00; 
       0x11; 0x00; 0x11; 0x00; 0x11; 0x00; 0x1f; 0x00; 
    |];
  set = Cursor.ints_to_bytes
    [| 0x00; 0x00; 0x7c; 0x00; 0x70; 0x00; 0x78; 0x00; 
       0x5f; 0xfe; 0x4f; 0xfe; 0x0f; 0xfe; 0x0e; 0x00; 
       0x0e; 0x00; 0x0e; 0x00; 0x0e; 0x00; 0x0e; 0x00; 
       0x0e; 0x00; 0x0e; 0x00; 0x0e; 0x00; 0x00; 0x00; 
    |];
}
@

<<constant [[Cursors.t]]>>=
let t = {
  offset =  {x = -7; y = -8; };
  clr = Cursor.ints_to_bytes
    [| 0x00; 0x00; 0x00; 0x00; 0x03; 0x80; 0x06; 0xc0; 
       0x1c; 0x70; 0x10; 0x10; 0x0c; 0x60; 0xfc; 0x7f; 
       0x80; 0x01; 0x80; 0x01; 0x80; 0x01; 0xff; 0xff; 
       0x00; 0x00; 0x00; 0x00; 0x00; 0x00; 0x00; 0x00; 
    |];
  set = Cursor.ints_to_bytes
    [| 0x00; 0x00; 0x00; 0x00; 0x00; 0x00; 0x01; 0x00; 
       0x03; 0x80; 0x0f; 0xe0; 0x03; 0x80; 0x03; 0x80; 
       0x7f; 0xfe; 0x7f; 0xfe; 0x7f; 0xfe; 0x00; 0x00; 
       0x00; 0x00; 0x00; 0x00; 0x00; 0x00; 0x00; 0x00; 
    |];
}
@

<<constant [[Cursors.tr]]>>=
let tr = {
  offset =  {x = -11; y = -4; };
  clr = Cursor.ints_to_bytes
    [| 0x00; 0x7f; 0x00; 0x41; 0x00; 0x31; 0xff; 0xe1; 
       0x80; 0x05; 0x80; 0x0d; 0x80; 0x0b; 0xff; 0x88; 
       0x00; 0x88; 0x0; 0x88; 0x00; 0x88; 0x00; 0x88; 
       0x00; 0x88; 0x00; 0x88; 0x00; 0x88; 0x00; 0xf8; 
    |];
  set = Cursor.ints_to_bytes
    [| 0x00; 0x00; 0x00; 0x3e; 0x00; 0x0e; 0x00; 0x1e; 
       0x7f; 0xfa; 0x7f; 0xf2; 0x7f; 0xf0; 0x00; 0x70; 
       0x00; 0x70; 0x00; 0x70; 0x00; 0x70; 0x00; 0x70; 
       0x00; 0x70; 0x00; 0x70; 0x00; 0x70; 0x00; 0x00; 
    |];
}
@

<<constant [[Cursors.r]]>>=
let r = {
  offset =  {x = -8; y = -7; };
  clr = Cursor.ints_to_bytes
    [| 0x07; 0xc0; 0x04; 0x40; 0x04; 0x40; 0x04; 0x58; 
       0x04; 0x68; 0x04; 0x6c; 0x04; 0x06; 0x04; 0x02; 
       0x04; 0x06; 0x04; 0x6c; 0x04; 0x68; 0x04; 0x58; 
       0x04; 0x40; 0x04; 0x40; 0x04; 0x40; 0x07; 0xc0; 
    |];
  set = Cursor.ints_to_bytes
    [| 0x00; 0x00; 0x03; 0x80; 0x03; 0x80; 0x03; 0x80; 
       0x03; 0x90; 0x03; 0x90; 0x03; 0xf8; 0x03; 0xfc; 
       0x03; 0xf8; 0x03; 0x90; 0x03; 0x90; 0x03; 0x80; 
       0x03; 0x80; 0x03; 0x80; 0x03; 0x80; 0x00; 0x00; 
    |];
}
@

<<constant [[Cursors.br]]>>=
let br = {
  offset =  {x = -11; y = -11; };
  clr = Cursor.ints_to_bytes
    [| 0x00; 0xf8; 0x00; 0x88; 0x00; 0x88; 0x00; 0x88; 
       0x00; 0x88; 0x00; 0x88; 0x00; 0x88; 0x00; 0x88; 
       0xff; 0x88; 0x80; 0x0b; 0x80; 0x0d; 0x80; 0x05; 
       0xff; 0xe1; 0x00; 0x31; 0x00; 0x41; 0x00; 0x7f; 
    |];
  set = Cursor.ints_to_bytes
    [| 0x00; 0x00; 0x00; 0x70; 0x00; 0x70; 0x00; 0x70; 
       0x0; 0x70; 0x00; 0x70; 0x00; 0x70; 0x00; 0x70; 
       0x00; 0x70; 0x7f; 0xf0; 0x7f; 0xf2; 0x7f; 0xfa; 
       0x00; 0x1e; 0x00; 0x0e; 0x00; 0x3e; 0x00; 0x00; 
    |];
}
@

<<constant [[Cursors.b]]>>=
let b = {
  offset =  {x = -7; y = -7; };
  clr = Cursor.ints_to_bytes
    [| 0x00; 0x00; 0x00; 0x00; 0x00; 0x00; 0x00; 0x00; 
       0xff; 0xff; 0x80; 0x01; 0x80; 0x01; 0x80; 0x01; 
       0xfc; 0x7f; 0x0c; 0x60; 0x10; 0x10; 0x1c; 0x70; 
       0x06; 0xc0; 0x03; 0x80; 0x00; 0x00; 0x00; 0x00; 
    |];
  set = Cursor.ints_to_bytes
    [| 0x00; 0x00; 0x00; 0x00; 0x00; 0x00; 0x00; 0x00; 
       0x00; 0x00; 0x7f; 0xfe; 0x7f; 0xfe; 0x7f; 0xfe; 
       0x03; 0x80; 0x03; 0x80; 0x0f; 0xe0; 0x03; 0x80; 
       0x01; 0x00; 0x00; 0x00; 0x00; 0x00; 0x00; 0x00; 
    |];
}
@

<<constant [[Cursors.bl]]>>=
let bl = {
  offset =  {x = -4; y = -11; };
  clr = Cursor.ints_to_bytes
    [| 0x1f; 0x00; 0x11; 0x00; 0x11; 0x00; 0x11; 0x00; 
       0x11; 0x00; 0x11; 0x00; 0x11; 0x00; 0x11; 0x00; 
       0x11; 0xff; 0xd0; 0x01; 0xb0; 0x01; 0xa0; 0x01; 
       0x87; 0xff; 0x8c; 0x00; 0x82; 0x00; 0xfe; 0x00; 
    |];
  set = Cursor.ints_to_bytes
    [| 0x00; 0x00; 0x0e; 0x00; 0x0e; 0x00; 0x0e; 0x00; 
       0x0e; 0x00; 0x0e; 0x00; 0x0e; 0x00; 0x0e; 0x00; 
       0x0e; 0x00; 0x0f; 0xfe; 0x4f; 0xfe; 0x5f; 0xfe; 
       0x78; 0x00; 0x70; 0x00; 0x7c; 0x00; 0x00; 0x0; 
    |];
}
@

<<constant [[Cursors.l]]>>=
let l = {
  offset =  {x = -7; y = -7; };
  clr = Cursor.ints_to_bytes
    [| 0x03; 0xe0; 0x02; 0x20; 0x02; 0x20; 0x1a; 0x20; 
       0x16; 0x20; 0x36; 0x20; 0x60; 0x20; 0x40; 0x20; 
       0x60; 0x20; 0x36; 0x20; 0x16; 0x20; 0x1a; 0x20; 
       0x02; 0x20; 0x02; 0x20; 0x02; 0x20; 0x03; 0xe0; 
    |];
  set = Cursor.ints_to_bytes
    [| 0x00; 0x00; 0x01; 0xc0; 0x01; 0xc0; 0x01; 0xc0; 
       0x09; 0xc0; 0x09; 0xc0; 0x1f; 0xc0; 0x3f; 0xc0; 
       0x1f; 0xc0; 0x09; 0xc0; 0x09; 0xc0; 0x01; 0xc0; 
       0x01; 0xc0; 0x01; 0xc0; 0x01; 0xc0; 0x00; 0x00; 
    |];
}
@

\section{Setting the cursor}

\subsection{[[riosetcursor()]]}


\subsection{[[cornercursor()]]}

<<function [[Wm._corner_cursor]]>>=
let _corner_cursor (w : Window.t) (pt : Point.t) (mouse : Mouse.ctl) =
  if Window.pt_on_border pt w
  then Mouse.set_cursor mouse (Cursors.which_corner_cursor w.screenr pt)
@


<<function [[Wm.corner_cursor_or_window_cursor]]>>=
let corner_cursor_or_window_cursor (w : Window.t) (pt : Point.t) (mouse : Mouse.ctl) =
  if Window.pt_on_border pt w
  then Mouse.set_cursor mouse (Cursors.which_corner_cursor w.screenr pt)
  else window_cursor w pt mouse
@

<<function [[Window.pt_on_border]]>>=
(* old: was called winborder in rio *)
let pt_on_border pt w =
  Rectangle.pt_in_rect pt w.screenr && not (pt_inside_border pt w)
@

<<function [[Window.pt_inside_border]]>>=
(* old: was not an helper in rio, but should to be consistent with winborder.
 * alt: pt_on_content (window border vs window content in Windows.nw)
 *)
let pt_inside_border pt w =
  Rectangle.pt_in_rect pt (Rectangle.insetrect window_border_size w.screenr)
@




<<signature [[Cursors.which_corner_cursor]]>>=
val which_corner_cursor: Rectangle.t -> Point.t -> Cursor.t
@


<<function [[Cursors.which_corner_cursor]]>>=
let which_corner_cursor rect p =
  let corner = which_corner rect p in
  match corner with
  | TopLeft -> tl
  | Top -> t
  | TopRight -> tr
  | Left -> l
  | Right -> r
  | BottomLeft -> bl
  | Bottom -> b
  | BottomRight -> br
@

<<type [[Cursors.portion]]>>=
type portion = Inf | Middle | Sup
@

<<function [[Cursors.which_corner]]>>=
let which_corner r p =
  let left_right = portion p.x r.min.x r.max.x in
  let bottom_top = portion p.y r.min.y r.max.y in
  match left_right, bottom_top with
  | Inf, Inf -> TopLeft
  | Inf, Middle -> Left
  | Inf, Sup -> BottomLeft
  | Middle, Inf -> Top
  | Middle, Middle -> 
    raise (Impossible "which_corner: pt_on_frame should not generate this")
  | Middle, Sup -> Bottom
  | Sup, Inf -> TopRight
  | Sup, Middle -> Right
  | Sup, Sup -> BottomRight
@

<<function [[Cursors.portion]]>>=
let portion x low high =
  (* normalize to low *)
  let x = x - low in
  let high = high - low in
  (* TODO: unused low? *)
  let _low = 0 in

  match () with
  | _ when x < 20 -> Inf
  | _ when x > high - 20 -> Sup
  | _ -> Middle
@


\subsection{[[wsetcursor()]]}

%******************************************************************************
\chapter{Window Manager}
%******************************************************************************
\label{chap:wm}

\section{Overview}

\section{Right-click system menu}

<<[[Thread_mouse.thread()]] match [[under_mouse]] cases>>=
| (Nothing | CurrentWin _) when m.buttons.right ->
  wm_menu caps m.pos Mouse.Right exitchan 
    mouse (display, desktop, view, font) fs
@

<<function [[Thread_mouse.wm_menu]]>>=
(* bind to right-click *)
let wm_menu (caps : < Cap.fork; .. >) (pos : Point.t) button
     (exitchan : Exit.t Event.channel) 
     (mouse : Mouse.ctl) 
     (display, desktop, view, font) 
     (fs : Fileserver.t) =
  (* todo: set (and later restore) sweeping to true *)

  let items = [
    <<[[Thread_mouse.wm_menu()]] [[items]] elements>>
  ] @
    <<[[Thread_mouse.wm_menu()]] [[items]] hidden window elements>>
  in
  Menu_ui.menu items pos button
    mouse (display, desktop, view, font)
@

<<[[Thread_mouse.wm_menu()]] [[items]] elements>>=
"Exit", (fun () ->
  Event.send exitchan Exit.OK |> Event.sync;
);
@
% this will wakeup thread_main who was waiting on exitchan


\section{Window borders click}

\section{[[Wctlmesg]]}


\section{Window creation}

<<[[Thread_mouse.wm_menu()]] [[items]] elements>>=
(* less: the first item get selected the very first time; QEMU bug?  *)
"New", (fun () ->
  let img_opt = Mouse_action.sweep mouse (display, desktop, font) in
  img_opt |> Option.iter (fun img ->
     Wm.new_win caps img "/bin/rc" [|"rc"; "-i"|] None (mouse, fs, font)
    (* 
       Wm.new_win img "/tests/xxx/test_rio_graph_app1" 
         [|"/tests/xxx/test_rio_graph_app1"|] None (mouse, fs, font)
       Wm.new_win img "/tests/rio/8.out" 
         [|"/tests/rio/8.out"|] None (mouse, fs, font)
       Wm.new_win img "/tests/xxx/hellorio" 
         [|"/tests/xxx/hellorio"|] None (mouse, fs, font)
       Wm.new_win img "/tests/xxx/test_rio_console_app1" 
         [|"/tests/xxx/test_rio_console_app1"|] None (mouse, fs, font)
    *)
  )
);
@

<<signature [[Wm.new_win]]>>=
(* This is called from Thread_mouse.ml when right-click and select New.
 * Internally it will:
 * - create a new Window.t and adds it to Globals.windows
 * - create a new window thread
 * - fork a new process, mount /mnt/wsys from fileserver and then binds
 *   /mnt/wsys to /dev so the process will have virtual /dev/{cons,winname,...}
 *   and finally run the cmd in it
 *)
val new_win:
  < Cap.fork; Cap.exec; Cap.chdir; Cap.mount; Cap.bind; .. > ->
  Image.t -> string (* cmd *) -> string array (* argv (including argv0) *) ->
  Fpath.t option (* pwd option *) ->
  (Mouse.ctl * Fileserver.t * Font.t) ->
  unit
@

\subsection{Window thread creation}%%: [[new()]]}

<<function [[Wm.new_win]]>>=
(* New window! new process! new thread!
 *
 * less: hideit, pid (but 0, or if != 0 -> use another func), scrolling
 *
 * CLI.thread_main -> Thread_mouse.thread -> Thread_mouse.wm_menu -> <>
 *)
let new_win (caps: < Cap.fork; Cap.exec; Cap.chdir; ..>) (img : Image.t)
    (cmd : string) (argv : string array) (pwd_opt : Fpath.t option)
    (_mouse, fs, font) : unit =

  (* A new Window.t *)

  (* less: cpid channel?  *)
  (* less: scrollit *)

  let w : Window.t = Window.alloc img font in

  (* less: wscrdraw here? (instead of in alloc, ugly) and draw(cols[BACK])? *)
  (* less: incref? *)

  (* simpler: draw_border w Window.Selected;
   * but done already later in set_current_and_repaint_borders
   *)

  (* A new window thread *)

  Hashtbl.add Globals.windows w.id w;
  let _win_thread = Thread.create !threads_window_thread_func w in

  (* less: if not hideit *)
  set_current_and_repaint (Some w);
  Image.flush img;

  (* A new window process *)

  pwd_opt |> Option.iter (fun str -> w.pwd <- str);

  (* TODO: Thread.critical_section := true; *)
  (* Logs.warn (fun m -> m "TODO: Thread.critical_section"); *)

  let res = CapUnix.fork caps () in
  (match res with
  | -1 -> failwith "fork returned -1"
  | 0 ->
    (* child *)
    Processes_winshell.run_cmd_in_window_in_child_of_fork caps cmd argv w fs
  | pid -> 
    <<[[Wm.new_win()]] when parent in fork with [[pid]] child>>
  )
@

<<[[Wm.new_win()]] when parent in fork with [[pid]] child>>=
(* parent *)
(* TODO: Thread.critical_section := false; *)
w.pid <- pid;

(* todo: how know if pb in child that require us then from
 * delete the window? need a cpid!
 *)

(* old: was in wsetpid() *)
w.label <- spf "rc %d" pid;
(* less: notefd *)

(* less: not too late? race with child to access /dev/winname? *)
let winname = spf "window.%d" w.id in
w.winname <- winname;
Draw_ipc.name_image w.img winname;
(* less: namecount and retry again if already used *)
@


<<[[Window.t]] process fields>>=
(* not really mutable, but set after Window.alloc() *)
mutable pid: int;
@
% useful for?

<<[[Window.t]] process fields>>=
(* can be changed through /mnt/wsys/wdir *)
mutable pwd: Fpath.t;
(* todo? notefd *)
@



<<signature [[Wm.threads_window_thread_func]]>>=
(* for mutual dependencies in new_win *)
val threads_window_thread_func: (Window.t -> unit) ref
@
% must be done before we create thhe mouse thread?
<<global [[Wm.threads_window_thread_func]]>>=
(* will be set to Threads_window.thread in CLI.ml *)
let threads_window_thread_func: (Window.t -> unit) ref = ref (fun _ ->
  failwith "threads_window_thread_func undefined"
)
@
<<[[CLI.thread_main()]] threads creation>>=
(* To break some mutual dependencies.
 * The mouse right-click and menu will trigger the creation
 * of new windows and new window threads and call this function.
 *)
Wm.threads_window_thread_func := Threads_window.thread;
@
% what is the actual mutual dep? X -> Y -> X ?


<<signature [[Wm.set_current_and_repaint]]>>=
(* this also used to have a mouse parameter (should be renamed then
 * set_current_and_repaint_borders_content_and_cursor)
 *)
val set_current_and_repaint:
  Window.t option -> unit
@


\subsection{[[Window]] allocation}%%: [[wmk()]]}

<<function [[Window.alloc]]>>=
(* ... -> Thread_mouse.wm_menu -> Wm.new_win -> <> *)
let alloc (img : Display.image) (font : Font.t) : t = 
  incr wid_counter;
  incr topped_counter;

  let w = 
  { 
    id = !wid_counter;
    (* will be set in caller *)
    winname = "";
    label = "<unnamed>";

    (* set later in Wm.ml in the caller *)
    pid = -1;
    pwd = Fpath.v (Sys.getcwd ());

    img = img;
    screenr = img.r;

    topped = !topped_counter;

    mouse_opened   = false;
    consctl_opened = false;
    raw_mode       = false;

    deleted = false;

    mouse_cursor = None;

    chan_mouse    = Event.new_channel ();
    chan_keyboard = Event.new_channel ();
    chan_cmd      = Event.new_channel ();

    chan_devmouse_read = Event.new_channel ();
    mouseclicks_queue = Queue.create ();

    mouse_counter = 0;
    last_count_sent = 0;
    last_mouse = Mouse.fake_state;
    last_buttons = Mouse.nobuttons;

    chan_devcons_read  = Event.new_channel ();
    chan_devcons_write = Event.new_channel ();
    raw_keys = Queue.create ();

    terminal = Terminal.alloc img font;

    auto_scroll = false;
  }
  in
  w
@


\subsection{Window process creation}%%: [[winshell()]]}

% seen fork before and when in child call function below:

<<signature [[Processes_winshell.run_cmd_in_window_in_child_of_fork]]>>=
val run_cmd_in_window_in_child_of_fork:
  < Cap.chdir; Cap.exec; Cap.mount; Cap.bind; .. > ->
  string -> string array -> Window.t -> Fileserver.t -> unit
@

<<function [[Processes_winshell.run_cmd_in_window_in_child_of_fork]]>>=
let run_cmd_in_window_in_child_of_fork
     (caps : < Cap.chdir; Cap.exec; Cap.mount; Cap.bind; .. >)
     (cmd : string) (argv : string array)
     (w : Window.t) (fs : Fileserver.t) =

  (*Unix1*)CapUnix.chdir caps !!(w.pwd);
  (* todo? rfork for copy of namespace/fd/env, but ape fork does that? *)
    
  (*/* close server end so mount won't hang if exiting */*)
  Unix1.close fs.server_fd;
  (* todo: close on exec? *)

  Plan9.mount caps fs.clients_fd (-1) (Fpath.v "/mnt/wsys") Plan9.MRepl (spf "%d" w.id);
  Plan9.bind caps (Fpath.v "/mnt/wsys") (Fpath.v "/dev") Plan9.MBefore;

  (* less: wclose for ref counting *)
  (* todo: handle errors? Unix_error? then communicate failure to parent? *)
  (* bugfix: do not forget the last perm argument below! otherwise partial
   * application and stdin/stdout are never reopened (in fact the dup
   * below even crashes the plan9 kernel)
   *)
  Unix1.close Unix1.stdin;
  let _fd = Unix1.openfile "/dev/cons" [Unix1.O_RDONLY] 0o666 in
  (* TODO? assert fd = 0? possible in OCaml? *)
  Unix1.close Unix1.stdout;
  let _fd = Unix1.openfile "/dev/cons" [Unix1.O_WRONLY] 0o666 in
  (* TODO? assert fd = 1? possible in OCaml? *)
  Unix1.dup2 Unix1.stdout Unix1.stderr; 

  (* less: notify nil *)
  (*Unix2*)CapUnix.execv caps cmd argv |> ignore;
  (* should never reach this point *)
  failwith "exec failed"
@
% classic fork/adjust/exec idiom of unix, but here adjust is using mount
% for filesystem in userspace magic!

% pass down w.id so fileserver knows when see an Attach from which windows
% it comes from! and any other request like Walk will start from
% root fid where we have the window, so can propagate the info to new
% fids

\subsection{Namespace adjustments}%%: [[filsysmount()]]}

\subsection{Public layer: [[wsetname()]]}

\subsection{Window painting}
%ocaml: new section

<<function [[Wm.set_current_and_repaint]]>>=
(* old: was called wcurrent() in rio.
 * alt: this function also sets the window cursor in rio-C, but this
 * requires then to pass a mouse parameter, which in turn requires to
 * pass the mouse in Reshape, which then requires to pass the mouse
 * parameter to hide_win and show_win and other functions which is
 * not super elegant. This is why I prefer to not pass a mouse parameter
 * which means the user may have to move the mouse to see the cursor
 * correctly updated after certain wm operations. I think this
 * tradeoff is ok.
 *)
let set_current_and_repaint wopt (*mouse*) =
  (* less: if wkeyboard *)
  let old = !Globals.current in
  Globals.current := wopt;
  (match old, wopt with
  | Some w2, Some w when not (w2 == w) ->
    (* bugfix: was doing repaint w, hmm *)
    repaint w2
  | _ -> ()
  );
  wopt |> Option.iter (fun w ->
    repaint w;
    (* TODO: do that in caller? so no need pass mouse? *)
    (* window_cursor w ptTODO mouse;*)

    (* todo: wakeup? why? *)
    ()
  )
@

<<function [[Wm.repaint]]>>=
(* old: was called wrepaint in rio *)
let repaint (w : Window.t) =
  let status = 
    match Globals.win () with
    | Some w2 when w2 == w -> Window.Selected
    | _ -> Window.Unselected
  in
  draw_border w status;
  (* less: wsetcursor again? *)
  w.terminal.Terminal.is_selected <- (status = Window.Selected);
  if not w.mouse_opened 
  then Terminal.repaint w.terminal 
@
% terminal put in different grey when window has focus or not

<<type [[Window.border_status]]>>=
type border_status = 
  | Selected
  | Unselected
@

<<function [[Wm.draw_border]]>>=
let draw_border (w : Window.t) (status : Window.border_status) =
  let img : Display.image = w.img in
  (* less: if holding? *)
  let color = 
    match status with
    | Window.Selected   -> !Globals.title_color
    | Window.Unselected -> !Globals.title_color_light
  in
  Polygon.border img img.r Window.window_border_size color Point.zero
@

<<signature [[Terminal.repaint]]>>=
val repaint: t -> unit
@

\subsection{Mouse action [[sweep()]]}

<<signature [[Mouse_action.sweep]]>>=
(* allows to specify a window area *)
val sweep: 
  Mouse.ctl -> (Display.t * Baselayer.t * Font.t) -> Image.t option
@

<<type [[Mouse_action.sweep_state]]>>=
type sweep_state = 
  (* start, no buttons *)
  | SweepInit
  (* right click *)
  | SweepRightClicked of Point.t
  (* move while holding right click *)
  | SweepMove of Point.t * Point.t * Image.t option
  (* release right click *)
  | SweepUnclicked of Image.t option
  <<[[Mouse_action.sweep_state]] other cases>>
@

%XXX
<<[[Mouse_action.sweep_state]] other cases>>=
(* to factorize cornercursor *)
| SweepReturn of Image.t option

(* error management when left or middle click or too small rectangle *)
| SweepRescue of bool (* clicked state *) * Image.t option
(* wait until no buttons *)
| SweepDrain
@

<<function [[Mouse_action.sweep]]>>=
let sweep (mouse : Mouse.ctl) (display, desktop, font) : Image.t option =
  (* todo: menuing? but not sweeping? *)
  Mouse.set_cursor mouse Cursors.crosscursor;

  let rec transit = function
    <<[[Mouse_action.sweep()]] match [[sweep_state]] cases>>
  in
  transit SweepInit
@


<<[[Mouse_action.sweep()]] match [[sweep_state]] cases>>=
| SweepInit ->
  let m = Mouse.read mouse in
  if not (Mouse.has_click m)
  then transit SweepInit
  else 
    (* less: force exactly right click? *)
    (* todo: Mouse.Right at some point *)
    if Mouse.has_button m Mouse.Left
    then transit (SweepRightClicked m.Mouse.pos)
    else transit (SweepRescue (true, None))
@
<<[[Mouse_action.sweep()]] match [[sweep_state]] cases>>=
| SweepRightClicked p0 ->
  (* less: onsceen (using clipr) *)
  transit (SweepMove (p0, p0, None))
@
<<[[Mouse_action.sweep()]] match [[sweep_state]] cases>>=
| SweepMove (p0, p1, old_img_opt) ->
  let m = Mouse.flush_and_read display mouse in
  (match () with
  (* less: force exactly right click? *)
  (* todo: Mouse.Right at some point *)
  | _ when Mouse.has_button m Mouse.Left ->
    if m.Mouse.pos = p1
    then transit (SweepMove (p0, p1, old_img_opt))
    else
      (* less: onscreen *)
      let p1 = m.Mouse.pos in
      let r = Rectangle.canonical p0 p1 in
      if Rectangle.dx r > 5 && Rectangle.dy r > 5 then begin
        (* need that? should be transparent anyway no? *)
        let grey = Color.mk2 0xEE 0xEE 0xEE in
        (* todo? RefreshNothing? *)
        let img = Layer.alloc desktop r grey in
        old_img_opt |> Option.iter Layer.free;
        Polygon.border img r Window.window_border_size 
          !Globals.red Point.zero;
        Display.flush display;
        transit (SweepMove (p0, p1, Some img))
      end
      else transit (SweepMove (p0, p1, old_img_opt))
  | _ when not (Mouse.has_click m) -> 
    transit (SweepUnclicked (old_img_opt))
  | _ -> 
    assert(Mouse.has_click m);
    transit (SweepRescue (true, old_img_opt))
  )
@
<<[[Mouse_action.sweep()]] match [[sweep_state]] cases>>=
| SweepUnclicked (old_img_opt) ->
  (match old_img_opt with
  | None -> transit (SweepReturn None)
  | Some (old_img : Display.image) ->
    let r = old_img.r in
    if Rectangle.dx r < 100 || Rectangle.dy r < 3 * font.Font.height
    then transit (SweepRescue (false, old_img_opt))
    else begin
      (* todo? RefreshBackup? *)
      let img = Layer.alloc desktop r Color.white in
      Layer.free old_img;
      Polygon.border img r Window.window_border_size 
        !Globals.red Point.zero;
      (* done in caller but more logical here I think *)
      Display.flush display;
      (* finally!! got an image *)
      transit (SweepReturn (Some img))
    end
  )
@



%XXX
<<[[Mouse_action.sweep()]] match [[sweep_state]] cases>>=
| SweepReturn img_opt ->
  (* todo: cornercursor? pos! *)
  (* less: moveto to force cursor update? ugly ... *)
  (* less: menuing = false *)
  img_opt
@
<<[[Mouse_action.sweep()]] match [[sweep_state]] cases>>=
| SweepRescue (clicked_state, old_img_opt) ->
  old_img_opt |> Option.iter Layer.free;
  if clicked_state
  then transit SweepDrain
  else transit (SweepReturn None)
@
<<[[Mouse_action.sweep()]] match [[sweep_state]] cases>>=
| SweepDrain ->
  let m = Mouse.read mouse in
  if not (Mouse.has_click m)
  then transit (SweepReturn None)
  else transit SweepDrain
@


\section{Window focus}

<<[[Thread_mouse.thread()]] match [[under_mouse]] cases>>=
| OtherWin w when m.buttons.left ->
  Wm.top_win w
  (* less: should drain and wait that release up, unless winborder *)
@

<<[[Thread_mouse.thread()]] match [[under_mouse]] cases>>=
| OtherWin w when m.buttons.middle || m.buttons.right ->
  Wm.top_win w
  (* todo: should goto again, may need to send event *)
@

<<signature [[Wm.top_win]]>>=
val top_win:
  Window.t -> unit
@


<<function [[Wm.top_win]]>>=
let top_win (w : Window.t) =
  if w.topped = !Window.topped_counter
  then ()
  else begin
    Layer.put_to_top w.img;
    set_current_and_repaint (Some w);
    Image.flush w.img;

    incr Window.topped_counter;
    w.topped <- !Window.topped_counter;
  end
@


\section{Window deletion}

<<[[Thread_mouse.wm_menu()]] [[items]] elements>>=
"Delete", (fun () -> 
  let wopt = Mouse_action.point_to mouse in
  wopt |> Option.iter (fun (w : Window.t) ->
    let cmd = Window.Delete in
    Event.send w.chan_cmd cmd |> Event.sync;
  ));
@

<<[[Threads_window.cmd_in()]] match [[cmd]] cases>>=
| Delete -> 
  (* less: break if window already deleted *)
  (* todo: delete timeout process *)
  Wm.close_win w
@

<<signature [[Wm.close_win]]>>=
val close_win:
  Window.t -> unit
@

<<function [[Wm.close_win]]>>=
let close_win (w : Window.t) =
  w.deleted <- true;
  Globals.win () |> Option.iter (fun w2 ->
    if w2 == w
    then Globals.current := None;
    (* less: window_cursor  ?*)
  );
  (* less: if wkeyboard *)
  Hashtbl.remove Globals.hidden w.id;
  Hashtbl.remove Globals.windows w.id;
  Layer.free w.img;
  w.img <- Image.fake_image;
  ()
@

% who will terminate the window thread?

%%\subsection{[[delete()]]}
%%\subsection{[[deletetimeoutproc()]] and [[deletethread()]]}
%%\subsection{[[Deleted]] and [[Exited]]}


\subsection{Mouse action [[pointto()]]}

<<signature [[Mouse_action.point_to]]>>=
val point_to: 
  Mouse.ctl -> Window.t option
@

<<function [[Mouse_action.point_to]]>>=
let point_to (mouse : Mouse.ctl) : Window.t option =
  (* todo: menuing? but not sweeping? *)
  Mouse.set_cursor mouse Cursors.sightcursor;

  let m = ref (Mouse.read mouse) in
  while not (Mouse.has_click !m) do
    m := Mouse.read mouse;
  done;
  let wopt =
    if Mouse.has_button !m Mouse.Right
    then Globals.window_at_point !m.Mouse.pos
    else None
  in
  (* less: wait and cancel option? *)
  while (Mouse.has_click !m) do
    m := Mouse.read mouse;
  done;
  (* restore cursor state *)
  Globals.win () |> Option.iter (fun w ->
    Wm.corner_cursor_or_window_cursor w !m.Mouse.pos mouse
  );
  wopt
@

\section{Window move}

<<[[Thread_mouse.wm_menu()]] [[items]] elements>>=
"Move", (fun () -> raise Todo);
@

\subsection{[[move()]]}

\subsection{Mouse action [[drag()]]}


\section{Window resize}

<<[[Thread_mouse.wm_menu()]] [[items]] elements>>=
(* old: was Reshape but here it's really resizing *)
"Resize", (fun () -> raise Todo);
@

<<[[Window.cmd]] cases>>=
(* for resize event but also for hide/show *)
| Reshape of 
      Image.t (* can be Layer.t or an off-screen Image.t when hidden *)
      (*Mouse.ctl*) (* needed for window_cursor() when repaint border *)
@

<<[[Threads_window.cmd_in()]] match [[cmd]] cases>>=
| Reshape (new_img : Display.image) ->
  (* less: put all of that in Wm.resize_win ? *)

  if w.deleted
  (* less: free new_img if deleted, but when can happen? *)
  then failwith "window already deleted";

  let r = new_img.r in
  w.screenr <- r;
  Wm.resize_win w new_img;
  (* less: set wctlready to true *)
  (* todo: delete timeout proc for old name of window *)

  (match Rectangle.dx r, Globals.win () with
  | 0, Some w2 when w2 == w ->
    Wm.set_current_and_repaint None
  | _n, Some w2 when (w2 == w) -> 
    (* less: could Wm.set_current_and_repaint_borders (Some w) mouse,
     * useless opti I think to special case here w2 == w
     *)
    ()
  | _n, (Some _ | None) ->
    Wm.set_current_and_repaint (Some w)
  );
  (* less: Image.flush new_img, but useless cos done in thread () *)
  ()
@

<<signature [[Wm.resize_win]]>>=
val resize_win:
  Window.t -> Image.t -> unit
@

\subsection{[[resize()]]}

<<function [[Wm.resize_win]]>>=
(* less: move boolean parameter, useless opti test dx/dy below catch it *)
let resize_win (w : Window.t) (new_img : Display.image) =
  let old_img : Display.image = w.img in
  let old_r : Rectangle.t = old_img.r in
  let new_r : Rectangle.t = new_img.r in
  if Rectangle.dx old_r = Rectangle.dx new_r && 
     Rectangle.dy old_r = Rectangle.dy new_r
  then Draw.draw new_img new_r old_img None old_r.min;
  (* a layer or image, so when hiding this should make disappear the window *)
  Image.free old_img;
  (* less: screenr set in caller, but could do it here *)
  w.img <- new_img;
  (* todo: wsetname *)

  (* todo: textual window update *)
  draw_border w Window.Selected;
  incr Window.topped_counter;
  w.topped <- !Window.topped_counter;

  (* todo: w.W.resized <- true *)
  (* todo: mouse counter ++ so transmit resize event *)
  ()
@

\subsection{Mouse action [[bandsize()]]}


\section{Window visibility}
\label{sec:window-hide}

<<[[Thread_mouse.wm_menu()]] [[items]] elements>>=
"Hide", (fun () -> 
  let wopt = Mouse_action.point_to mouse in
  wopt |> Option.iter (fun w ->
    Wm.hide_win w
  ));
@

<<signature [[Wm.hide_win]]>>=
(* (those actions used to have a mouse parameter to also update the cursor) *)
val hide_win:
  Window.t ->  unit
@

\subsection{[[hidden]]}

<<global [[Globals.hidden]]>>=
(* a subset of 'windows' *)
let hidden: (Window.wid, Window.t) Hashtbl.t = Hashtbl_.create ()
@

\subsection{[[hide()]]}

<<function [[Wm.hide_win]]>>=
let hide_win (w : Window.t) =
  if Hashtbl.mem Globals.hidden w.id
  (* less: return -1? can happen if window thread take too much time
   * to respond to the Reshape command?
   *)
  then raise (Impossible "window already hidden");
  let old_layer : Display.image = w.img in
  let display = old_layer.display in
  (* this is an image! not a layer, so it will not be visible on screen *)
  let img = 
    Image.alloc display w.screenr old_layer.chans false Color.white in
  (* less: return 0 or 1 if can or can not allocate? *)
  Hashtbl.add Globals.hidden w.id w;
  let cmd = Window.Reshape img in
  Event.send w.chan_cmd cmd |> Event.sync;
  ()
@
% and what do you do with img? Reshape img so will copy its content?

\subsection{[[unhide()]]}

<<[[Thread_mouse.wm_menu()]] [[items]] hidden window elements>>=
(Globals.hidden |> Hashtbl_.to_list |> List.map (fun (_wid, (w : Window.t)) ->
    (* less: could sort by name, or time it was put in hidden hash *)
    w.label, (fun () -> 
      Wm.show_win w desktop
    )))
@

<<signature [[Wm.show_win]]>>=
val show_win:
  Window.t -> Baselayer.t -> unit
@

<<function [[Wm.show_win]]>>=
let show_win (w : Window.t) (desktop : Baselayer.t) =
  let old_img : Display.image = w.img in
  (* back to a layer *)
  let layer = Layer.alloc desktop old_img.r Color.white in
  Hashtbl.remove Globals.hidden w.id;
  let cmd = Window.Reshape layer in
  Event.send w.chan_cmd cmd |> Event.sync;
  ()
@

% lots of the heavy lifting is done by memlayer

% Reshape abused again

%******************************************************************************
\chapter{Filesystem Server}
%******************************************************************************
\label{chap:filesystem-server}

%%\section{Additional data structures}

%%\subsection{[[Qxxx]]}
%ocaml: use ADT
%%\subsection{[[dirtab]]}
%ocaml: toplevel_entries?

%%\section{[[filsysrespond()]]}

\section{Attach}

% when is this called? just after Plan9.mount of winshell?
% probably, and get passed the w.id!

<<[[Threads_fileserver.dispatch()]] match [[request_typ]] cases>>=
(* Attach *)
| T.Attach (rootfid, _auth_fid_opt, uname, aname) ->
  (* stricter: *)
  if Hashtbl.mem fs.fids rootfid
  then failwith (spf "Attach: fid already used: %d" rootfid);

  (match () with
  | _ when uname <> fs.user ->
    error fs req (spf "permission denied, %s <> %s" uname fs.user)
  | _ ->
  (* less: could do that in a worker thread (if use qlock) *)
  (* less: newlymade, qlock all *)
  (try
     let wid = int_of_string aname in
     let w = Hashtbl.find Globals.windows wid in

     let entry = File.root_entry in
     let file_id = entry.code, wid in
     let qid = File.qid_of_fileid file_id entry.type_ in

     let file = File.{
       fid = rootfid; 
       qid = qid; 
       entry = entry;
       opened = None;
       win = w;
     } in
     Hashtbl.add fs.fids rootfid file;
     answer fs {req with P9.typ = P9.R (R.Attach qid) }
   with _exn ->
      error fs req (spf "unknown id in attach: %s" aname)
  (* less: incref, qunlock *)
  ))
@

% win = w!!

% return qid that encode the file_id which itself encodes the wid

<<[[File.t]] other fields>>=
(* The qid is what is returned by the "server" to identify a file (or dir).
 * It is mutable because a fid can be 'walked' to point to another file
 * on the server.
 *)
mutable qid: Plan9.qid;
@

<<type [[File.fileid]]>>=
(* will generate a Qid.path *)
type fileid = filecode * Window.wid
@

<<function [[File.qid_of_fileid]]>>=
let qid_of_fileid file_id typ =
  { path = int_of_fileid file_id;
    typ = typ;
    vers = 0;
  }
@

<<function [[File.int_of_fileid]]>>=
let int_of_fileid (qxxx, wid) = 
  (wid lsl 8) lor
  int_of_filecode qxxx
@
% the window and then deviceid

<<function [[File.int_of_filecode]]>>=
let int_of_filecode = function
  | Dir Root -> 0
  | File WinName -> 1
  | File Mouse -> 2
  | File Cons -> 3
  | File ConsCtl -> 4
  | File WinId -> 5
  | File Text -> 6
@


%%\subsection{[[filsysattach()]]}
%%\subsection{[[xfidattach()]]}

\section{Walk}

<<[[Threads_fileserver.dispatch()]] match [[request_typ]] cases>>=
(* Walk *)
| T.Walk (fid, newfid_opt, xs) ->
  check_fid "walk" fid fs;
  let file : File.t = Hashtbl.find fs.fids fid in
  let wid = file.win.id in
  (match file.opened, newfid_opt with
  | Some _, _ ->
    error fs req "walk of open file"
  (* stricter? failwith or error? *)
  | _, Some newfid when Hashtbl.mem fs.fids newfid ->
    error fs req (spf "clone to busy fid: %d" newfid)
  | _ when List.length xs > P9.max_welem ->
    error fs req (spf "name too long: [%s]" (String.concat ";" xs))
  | _ ->
    let file =
      match newfid_opt with
      | None -> file
      | Some newfid ->
        (* clone *)
        (* less: incref on file.w *)
        let newfile = { file with
          File.fid = newfid; 
          File.opened = None 
          (* todo: nrpart? *)
        } in
        Hashtbl.add fs.fids newfid newfile;
        newfile
    in
    let rec walk qid (entry : File.dir_entry_short) acc xs =
      match xs with
      | [] -> qid, entry, List.rev acc
      | x::xs ->
        if qid.N.typ <> N.QTDir
        (* will be catched below and transformed in an 9P Rerror message *)
        then failwith "not a directory";

        (match entry.code, x with
        | _Qwsys, ".." -> failwith "walk: Todo '..'"
        | File.Dir File.Root, x ->
          let entry = 
            toplevel_entries |> List.find (fun (entry : File.dir_entry_short)->
                     entry.name = x)
          in
          let file_id = entry.code, wid in
          let qid = File.qid_of_fileid file_id entry.type_ in
          (* continue with other path elements *)
          walk qid entry (qid::acc) xs

        (* todo: Wsys, snarf *)
        | _ -> 
          raise (Impossible "should be catched by 'not a directory' above")
        )
    in
    (try 
      let final_qid, final_entry, qids = 
        walk file.qid file.entry [] xs
      in
      file.qid <- final_qid;
      file.entry <- final_entry;
      answer fs { req with P9.typ = P9.R (R.Walk qids) }
    with exn ->
      newfid_opt |> Option.iter (fun newfid ->
        Hashtbl.remove fs.fids newfid
      );
      (match exn with
      (* this can happen many times because /dev is a union-mount so
       * we get walk requests also for /dev/draw/... and other devices
       *)
      | Not_found -> 
        error fs req "file does not exist"
      | Failure s when s = "not a directory" ->
        error fs req "not a directory"
      (* internal error then *)
      | _ -> raise exn
      )
    )
  )
@

<<function [[Threads_fileserver.check_fid]]>>=
(* less: could be check_and_find_fid to factorize more code in dispatch() *)
let check_fid op fid (fs : Fileserver.t) =
  (* stricter: *)
  if not (Hashtbl.mem fs.fids fid)
  then failwith (spf "%s: unknown fid %d" op fid)
@

%%\subsection{[[filsyswalk()]]}

\subsection{Cloning [[fid]]}

\subsection{[[..]]}

\subsection{Error management}

\section{Open}

<<[[Threads_fileserver.dispatch()]] match [[request_typ]] cases>>=
(* Open *)
| T.Open (fid, flags) ->
  check_fid "open" fid fs;
  let file = Hashtbl.find fs.fids fid in
  let w = file.win in
  (* less: OTRUNC | OCEXEC | ORCLOSE, and remove DMDIR| DMAPPEND from perm *)
  let perm = file.entry.perm in
  if flags.x || (flags.r && not perm.r)
             || (flags.w && not perm.w)
  then error fs req "permission denied"
  else
    if w.deleted
    then error fs req "window deleted"
    else 
    (* less: could do that in a worker thread *)
    (try 
       (match file.entry.code with
       | File.File devid ->
         let dev = device_of_devid devid in
         dev.D.open_ w
       | File.Dir _dir ->
         (* todo: nothing to do for dir? ok to open a dir? *)
         ()
       );
       file.opened <- Some flags;
       let iounit = fs.message_size - P9.io_header_size in
       answer fs { req with P9.typ = P9.R (R.Open (file.qid, iounit)) }
     with Device.Error str ->
       error fs req str
    )
@

% device_of_devid

%%\subsection{[[filsysopen()]]}
%%\subsection{[[xfidopen()]]}

\section{Clunk/Close}

<<[[Threads_fileserver.dispatch()]] match [[request_typ]] cases>>=
(* Clunk *)
| T.Clunk (fid) ->
  check_fid "clunk" fid fs;
  let file = Hashtbl.find fs.fids fid in
  (match file.opened with
  | Some _flags ->
    (match file.entry.code with
    | File.File devid ->
      let dev = device_of_devid devid in
      dev.D.close file.win
      (* todo: wclose? *)
    | File.Dir _ ->
      ()
    )
  (* stricter? can clunk a file not opened? *)
  | None ->
    (* todo: winclosechan *)
    ()
  );
  Hashtbl.remove fs.fids fid;
  answer fs { req with P9.typ = P9.R (R.Clunk) }
@

%%\subsection{[[filsysclunk()]]}
%%\subsection{[[xfidclose()]]}

\section{Read}

<<[[Threads_fileserver.dispatch()]] match [[request_typ]] cases>>=
(* Read *)
| T.Read (fid, offset, count) ->
  check_fid "read" fid fs;
  let file = Hashtbl.find fs.fids fid in
  let w = file.win in

  (match file.entry.code with
  | _ when w.deleted ->
    error fs req "window deleted"
  | File.File devid ->
    (* a worker thread (less: opti: arena of workers? *)
    Thread.create (fun () ->
     (* less: getclock? *)
     (try 
       let dev = device_of_devid devid in
       let data = dev.D.read_threaded offset count w in
       answer fs { req with P9.typ = P9.R (R.Read data) }
     with Device.Error str ->
       error fs req str
    )) () |> ignore
  | File.Dir _ ->
    failwith "TODO: readdir"
  )
@

%%\subsection{[[filsysread()]]}
\subsection{Reading a directory}
%%\subsection{[[xfidread()]]}

\section{Write}

<<[[Threads_fileserver.dispatch()]] match [[request_typ]] cases>>=
(* Write *)
| T.Write (fid, offset, data) ->
  check_fid "write" fid fs;
  let file = Hashtbl.find fs.fids fid in
  let w = file.win in

  (match file.entry.code with
  | _ when w.deleted ->
    error fs req "window deleted"
  | File.File devid ->
    (* a worker thread (less: opti: arena of workers? *)
    Thread.create (fun () ->
     (try 
       let dev = device_of_devid devid in
       (* less: case where want to let device return different count? *)
       let count = String.length data in
       dev.D.write_threaded offset data w;
       answer fs { req with P9.typ = P9.R (R.Write count) }
     with Device.Error str ->
       error fs req str
    )) () |> ignore
  | File.Dir _ ->
    raise (Impossible "kernel should not call write on fid of a directory")
  )
@

%%\subsection{[[filsyswrite()]]}
%%\subsection{[[xfidwrite()]]}

\section{Stats}

<<[[Threads_fileserver.dispatch()]] match [[request_typ]] cases>>=
(* Stat *)
| T.Stat (fid) ->
  check_fid "stat" fid fs;
  let file = Hashtbl.find fs.fids fid in
  let short = file.entry in
  (* less: getclock *)
  let clock = 0 in
  let dir_entry = {
    N.name = short.name;
    (* less: adjust version for snarf *)
    N.qid = file.qid;
    N.mode = (N.int_of_perm_property short.perm, 
      match file.qid.N.typ with
      | N.QTDir -> N.DMDir
      | N.QTFile -> N.DMFile
    );
    N.length = 0; (*/* would be nice to do better */*)

    N.atime = clock;
    N.mtime = clock;

    N.uid = fs.user;
    N.gid = fs.user;
    N.muid = fs.user;

    N._typ = 0;
    N._dev = 0;
  }
  in
  answer fs { req with P9.typ = P9.R (R.Stat dir_entry) }
@

%%\subsection{[[filsysstat()]]}

\section{Forbidden operations}

%XXX
<<[[Threads_fileserver.dispatch()]] match [[request_typ]] cases>>=
(* Other *)
| T.Create _ 
| T.Remove _
| T.Wstat _
    -> error fs req "permission denied"
@
<<[[Threads_fileserver.dispatch()]] match [[request_typ]] cases>>=
(* todo: handle those one? *)
| T.Flush _
| T.Auth _
  -> 
  failwith (spf "TODO: req = %s" (P9.str_of_msg req))
@

%******************************************************************************
\chapter{Virtual Devices}
%******************************************************************************
\label{chap:virtual-devices}

\section{[[/mnt/wsys/mouse]]}

<<signature [[Virtual_mouse.dev_mouse]]>>=
(* a virtual "/dev/mouse" *)
val dev_mouse: Device.t
@

<<constant [[Virtual_mouse.dev_mouse]]>>=
let dev_mouse = { (*Device.default with*)
  name = "mouse";
  perm = Plan9.rw;

  <<method [[Virtual_mouse.dev_mouse.open_]]>>
  <<method [[Virtual_mouse.dev_mouse.close]]>>
  <<method [[Virtual_mouse.dev_mouse.read_threaded]]>>
  <<method [[Virtual_mouse.dev_mouse.write_threaded]]>>
}
@

% classic open/close with important mouse_opened field which tells
% the window manager that this is a graphical app
<<method [[Virtual_mouse.dev_mouse.open_]]>>=
open_ = (fun (w : Window.t) ->
  if w.mouse_opened
  then raise (Error "file in use");
  w.mouse_opened <- true;

  (* less: resized <- false? and race comment? *)
  ()
);
@

<<method [[Virtual_mouse.dev_mouse.close]]>>=
close = (fun (w : Window.t) ->
  (* stricter? check that was opened indeed? *)

  w.mouse_opened <- false;

  (* todo: resized? Refresh message?*)
  ()
);
@

\subsection{Reading part1}

<<method [[Virtual_mouse.dev_mouse.read_threaded]]>>=
(* a process is reading on its /dev/mouse; we must read from mouse
 * events coming to the window, events sent from the mouse thread.
 *)
read_threaded = (fun _offset count (w : Window.t) ->
  (* less: flushtag *)
  (* less: qlock active *)
  (* less: qlock unactive after answer? so need reorg this func? *)

  let chan = Event.receive w.chan_devmouse_read |> Event.sync in
  let m : Mouse.state    = Event.receive chan |> Event.sync in

  (* less: resize message *)

  let str = 
    spf "%c%11d %11d %11d %11d " 
      'm' m.pos.x m.pos.y (Mouse.int_of_buttons m.buttons) m.msec
  in
  (* bugfix: note that we do not honor_offset. /dev/mouse is a dynamic file *)
  Device.honor_count count str
);
@

<<[[Window.t]] mouse fields>>=
(* Threads_window.thread --> Thread_fileserver.dispatch(Read).
 * The channel inside the channel will be used to write a mouse state
 * to thread_fileserver.
*)
chan_devmouse_read: Mouse.state Event.channel Event.channel;
@


<<[[Threads_window.thread()]] other [[select]] elements>>=
(* sending *)
(if w.mouse_counter <> w.last_count_sent 
 then [Event.send w.chan_devmouse_read chan_devmouse 
        |> wrap (fun () -> SentChannelForMouseRead)]
 else []
) @
@
% Event.send! not Event.receive here, select can dispatch on
% receiving and sending if someone is ready on the other side to receive

<<[[Threads_window.thread()]] channels creation>>=
let chan_devmouse = Event.new_channel () in
@

<<[[Threads_window.event]] other cases>>=
(* producing for thread_fileserver(Read Qmouse) *)
| SentChannelForMouseRead
@

<<[[Threads_window.thread()]] match [[ev]] other cases>>=
| SentChannelForMouseRead -> 
  mouse_out w chan_devmouse
@


<<function [[Device.honor_count]]>>=
let honor_count count data =
  let len = String.length data in
  if len <= count
  then data
  else String.sub data 0 count
@

\subsection{Writing}

<<method [[Virtual_mouse.dev_mouse.write_threaded]]>>=
write_threaded = (fun _offset _str _w ->
  failwith "TODO: virtual_mouse.write_threaded"
);
@


\section{[[/mnt/wsys/cons]]}

<<signature [[Virtual_cons.dev_cons]]>>=
(* virtual "/dev/cons" *)
val dev_cons    : Device.t
@

<<constant [[Virtual_cons.dev_cons]]>>=
let dev_cons = { Device.default with
  name = "cons";
  perm = Plan9.rw;

  (* a process is reading on its /dev/cons; we must read from key
   * events coming to the window (events sent from the keyboard thread).
   *)
  read_threaded = (fun _offset count (w : Window.t) ->
    (* less: flushtag *)
    (* less: handle unicode partial runes *)
    let (chan_count_out, chan_bytes_in) = 
      Event.receive w.chan_devcons_read |> Event.sync in
    Event.send chan_count_out count |> Event.sync;
    (* less: handle if flushing *)
    (* less: qlock active *)
    let bytes = Event.receive chan_bytes_in |> Event.sync in
    (* we asked for count so honor_count is redundant below (but defensive) *)
    Device.honor_count count bytes
  );
  (* a process is writing on its /dev/cons; it wants to output strings
   * on the terminal
   *)
  write_threaded = (fun _offset str (w : Window.t) ->
    (* todo: partial runes *)
    let runes = Rune.bytes_to_runes str in
    (* less: flushtag *)
    let chan_runes_out = Event.receive w.chan_devcons_write |> Event.sync in
    (* less: handle if flushing *)
    (* less: qlock active *)
    Event.send chan_runes_out runes |> Event.sync;
  );
}
@

\subsection{Reading part1}

\subsection{Writing part1}

%%\subsection{Bytes versus runes, partial runes}

\section{[[/mnt/wsys/consctl]]}

<<signature [[Virtual_cons.dev_consctl]]>>=
(* virtual "/dev/consctl" *)
val dev_consctl : Device.t
@

<<constant [[Virtual_cons.dev_consctl]]>>=
let dev_consctl = { Device.default with
  name = "consctl";
  perm = Plan9.w;
  
  open_ = (fun (w : Window.t) ->
    if w.consctl_opened
    then raise (Error "file in use");
    w.consctl_opened <- true;
  );
  close = (fun (w : Window.t) ->
    (* less: if holding *)
    if w.raw_mode
    then begin
      w.raw_mode <- false;
      (* less: send RawOff? but nop in Threads_window anyway *)
    end;
    w.consctl_opened <- false;
  );
    
  write_threaded = (fun _offset str (w : Window.t) ->
    match str with
    | "rawon" -> 
      (* less: holding *)
      (* stricter: set to bool, not increment, so no support 
       *  for multiple rawon *)
      (* stricter? exn if already on? *)
      w.raw_mode <- true;
      (* less: send RawOn? *)
    | "rawoff" ->
      (* todo: send RawOff so terminal can process remaining queued raw keys *)
      w.raw_mode <- false;
    | "holdon" ->
      failwith ("TODO: holdon")
    | "holdoff" ->
      failwith ("TODO: holdoff")
    | _ -> 
      raise (Error (spf "unknown control message: %s" str));
  );
}
@

\section{[[/mnt/wsys/cursor]]}

<<[[Window.t]] graphics fields>>=
(* writable through /mnt/wsys/cursor *)
mutable mouse_cursor: Cursor.t option;
@

<<signature [[Virtual_mouse.dev_cursor]]>>=
(* a virtual "/dev/cursor" *)
(* val dev_cursor: Device.t *)
@

<<constant [[Virtual_mouse._dev_cursor]]>>=
let _dev_cursor = { (*Device.default with*)
  name = "cursor";
  perm = Plan9.rw;

  open_ = (fun _w ->
    raise Todo
  );
  close = (fun _w ->
    raise Todo
  );
  read_threaded = (fun _offset _count _w  ->
    raise Todo
  );
  write_threaded = (fun _offset _str _w ->
    raise Todo
  );
}
@



\section{[[/dev/draw/]] and [[/mnt/wsys/winname]]}

<<signature [[Virtual_draw.dev_winname]]>>=
(* virtual "/dev/winname" *)
val dev_winname: Device.t
@

<<constant [[Virtual_draw.dev_winname]]>>=
let dev_winname = { Device.default with
  name = "winname";
  perm = Plan9.r;
  
  read_threaded = (fun offset count (w : Window.t) ->
    let str = w.winname in
    if str = ""
    then raise (Error "window has no name");
    Device.honor_offset_and_count offset count str
  );
}
@

<<function [[Device.honor_offset_and_count]]>>=
let honor_offset_and_count offset count data =
  let len = String.length data in
  match () with
  | _ when offset > len -> ""
  | _ when offset + count > len ->
    String.sub data offset (len - offset)
  | _ -> data
@


%******************************************************************************
\chapter{Graphical Windows}
%******************************************************************************
\label{chap:graphical-windows}

\section{Graphical window setup}

\subsection{[[initdraw()]]}

\subsection{[[initmouse()]], mouse-open mode}

\subsection{[[initkeyboard()]], raw-access mode}

\section{Mouse events}

\subsection{Mouse state queue}

\subsection{[[/mnt/wsys/mouse]]  reading part2}

\section{Keyboard events}

\subsection{Raw keys queue}

\subsection{[[/mnt/wsys/cons]] reading part2}

\section{Resize events}

\section{[[/mnt/wsys/window]]}

%******************************************************************************
\chapter{Textual Windows}
%******************************************************************************
\label{chap:textual-windows}

\section{Textual window creation}

\subsection{Scrollbar}

\subsection{Frame}

\section{Frame widget}
\label{sec:frame-widget}

\subsection{[[Frame]]}

\subsection{[[frinit()]]}

\subsection{Frame colors}
\subsection{Frame tick}
\subsection{Frame boxes}
\subsection{Frame strings}
\subsection{Frame rune position, point, and box number}
\subsection{Frame selection}

\section{Content modification}

\subsection{[[winsert()]]}

\subsection{Growing array}

\section{Content rendering}

\subsection{[[frinsert()]]}

\subsection{[[frdelete()]]}

\subsection{[[wshow()]]}
\subsection{Drawing the scrollbar: [[wscrdraw()]]}
\subsection{Drawing the tick: [[frtick()]]}
\subsection{Drawing the text and the selection: [[frdrawsel()]]}
\subsection{Moving the frame origin}
\subsection{Selecting}
\subsection{Repainting}

\section{Keyboard events}

\subsection{Text input queue}

\subsection{[[/mnt/wsys/cons]] reading part3}

\subsection{Navigation keys}

\subsection{Special keys}

\section{Application output events}

\subsection{[[/mnt/wsys/cons]] writing part2}

\section{Mouse events}

\subsection{Middle click menu}

\subsection{Other clicks}

\section{Automatic scrolling mode}

<<[[Window.t]] config fields>>=
mutable auto_scroll: bool;
@

\section{Scroll bar interaction}

\section{Resize}



\section{[[/mnt/wsys/text]]}

<<signature [[Dev_textual_window.dev_text]]>>=
val dev_text: Device.t
@

<<constant [[Dev_textual_window.dev_text]]>>=
let dev_text = { Device.default with
  name = "text";
  perm = Plan9.r;
  read_threaded = (fun offset count w ->
    let term = w.W.terminal in 
    let str = Bytes.create term.T.nrunes in
    for i = 0 to term.T.nrunes - 1 do
      Bytes.set str i term.T.text.(i);
    done;
    Device.honor_offset_and_count offset count (Bytes.to_string str)
  );
}
@

%******************************************************************************
\chapter{Windowing System Files}
%******************************************************************************
\label{chap:window-files}

\section{[[/mnt/wsys/winid]]}

<<signature [[Dev_wm.dev_winid]]>>=
val dev_winid: Device.t
@

<<constant [[Dev_wm.dev_winid]]>>=
let dev_winid = { Device.default with
  name = "winid";
  perm = Plan9.r;
  read_threaded = (fun offset count (w : Window.t) ->
    let str = spf "%11d" w.id in
    Device.honor_offset_and_count offset count str
  );
}
@


\section{[[/mnt/wsys/label]]}

\section{[[/mnt/wsys/screen]]}

\section{[[/mnt/wsys/wsys/]]}

\section{[[/mnt/wsys/wctl]]}
\label{sec:mnt-wsys-wctl}

\subsection{Reading}

\subsection{Writing (controlling windows)}

%******************************************************************************
\chapter{Advanced Topics TODO}
%******************************************************************************
\label{chap:advanced}

\section{External mount: [[/srv/rio.user.pid]]}

\section{Command-line control: [[/srv/riowctl.user.pid]]}

\section{Recursive [[rio]]}
\label{sec:recursive-rio}

\section{Advanced terminal editing}

\subsection{Snarf}
\label{sec:snarf}

\subsection{Plumb}
\label{sec:plumb}

\subsection{Auto complete}
\label{sec:completion}
\label{sec:wdir}

\subsection{Word selection}

\section{Automatic scrolling: [[rio -s]]}

<<[[CLI.main()]] [[options]] elements>>=
"-s", Arg.Unit (fun () -> raise Todo),
" ";
@

\section{Initial command: [[rio -i]]}

<<[[CLI.main()]] [[options]] elements>>=
"-i", Arg.String (fun _s -> raise Todo),
" <initcmd>";
@

\section{Fake keyboard input: [[rio -k]]}

<<[[CLI.main()]] [[options]] elements>>=
"-k", Arg.String (fun _s -> raise Todo),
" <kbdcmd>";
@

\subsection{[[rio -k]]}

\subsection{[[wkeyboard]]}

\subsection{[[/mnt/wsys/kdbin]]}

\subsection{Keyboard hide}

\section{Font selection: [[rio -f]]}

<<[[CLI.main()]] [[options]] elements>>=
"-font", Arg.String (fun _s -> raise Todo),
" <fontname>";
@

\section{Holding mode}

\section{Signals, notes}

\section{Timer}

\section{Flushing}

\section{Security}

\section{TODO [[Wakeup]]}

\section{TODO [[Refresh]]}

%******************************************************************************
\chapter{Conclusion}
%******************************************************************************
\label{chap:conclusion}

%###############################################################################

\appendix

%******************************************************************************
\chapter{Debugging}
%******************************************************************************
\label{chap:debugging-appendix}


\section{Logging}

<<[[CLI.main()]] locals>>=
let level = ref (Some Logs.Warning) in
@

<<[[CLI.main()]] logging setup>>=
Logs_.setup !level ();
Logs.info (fun m -> m "rio ran from %s" (Sys.getcwd()));
@


<<constant [[Globals.debug_9P]]>>=
let debug_9P = ref false
@
<<[[CLI.main()]] [[options]] elements>>=
(* pad: not in original *)
"-debug_9P", Arg.Set Globals.debug_9P,
" ";
@

<<[[Threads_fileserver.thread]] in loop, debug>>=
(* todo: should exit the whole proc if error *)
if !Globals.debug_9P
then Logs.debug (fun m -> m "%s" (P9.str_of_msg req));
@

<<[[Threads_fileserver.answer()]] debug>>=
if !Globals.debug_9P
then Logs.debug (fun m -> m "%s" (P9.str_of_msg res));
@

<<constant [[Globals.debug_draw]]>>=
let debug_draw = ref false
@
<<[[CLI.main()]] [[options]] elements>>=
"-debug_draw", Arg.Set Globals.debug_draw,
" ";
@

<<[[CLI.thread_main()]] graphics initializations, debug>>=
if !Globals.debug_draw
then Display.debug display;
@


\section{Testing}

<<[[CLI.main()]] [[options]] elements>>=
"-test", Arg.Unit (fun () -> Test.test ()),
" ";
@

%******************************************************************************
\chapter{Error Management}
%******************************************************************************
\label{chap:error}

<<[[CLI.main()]] locals>>=
let backtrace = ref false in
@

<<[[CLI.main()]] handle [[exn]]>>=
if !backtrace
then raise exn
else 
  (match exn with
  | Failure s ->
        Logs.err (fun m -> m "%s" s);
        Exit.Code 1
  | _ -> raise exn
  )
@

<<function [[Threads_fileserver.error]]>>=
let error fs req str =
  let res = { req with P9.typ = P9.R (R.Error str) } in
  answer fs res
@


<<exception [[Device.Error]]>>=
(* This will be catched up by thread_fileserver to transform the
 * exception in an Rerror 9P response.
 *)
exception Error of string
@



%%\section{Error codes}
%%\section{[[error()]], [[derror()]]}

%******************************************************************************
%%\chapter{Utilities}
%******************************************************************************
%%\label{chap:utilities}

%******************************************************************************
\chapter{Examples of Windowing System Applications TODO}
%******************************************************************************
\label{chap:examples}

%******************************************************************************
\chapter{Extra Code}
%******************************************************************************

#include "orio_extra.nw"


\chapter*{Glossary}
\addcontentsline{toc}{chapter}{Glossary}
\label{sec:glossary}

\begin{verbatim}
API = Application Programming Interface
GUI = Graphical User Interface
IDE = Integrated Development Environment
IPC = Inter Process Communication
RPC = Remote Procedure Call
LOC = Lines Of Code
PTY = Pseudo TTY
TTY = Teletype
WIMP = Window Icon Menu Pointer
MIME = Multipurpose Internet Mail Extensions
PDA = Personal Digital Assistant
\end{verbatim}


\chapter*{Indexes}
\addcontentsline{toc}{chapter}{Index}

%src: wc.nw in noweb source
Here is a list of the identifiers used, and where they appear.
Underlined entries indicate the place of definition.
This index is generated automatically.

%\twocolumn does not work
\nowebindex

%\chapter{References} 
\addcontentsline{toc}{chapter}{References}

\bibliography{../docs/latex/Principia}
\bibliographystyle{alpha}

%******************************************************************************
% Postlude
%******************************************************************************

\end{document}
